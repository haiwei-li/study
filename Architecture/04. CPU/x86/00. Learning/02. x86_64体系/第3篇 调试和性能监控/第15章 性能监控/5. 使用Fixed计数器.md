
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->



<!-- /code_chunk_output -->

在版本 3 的性能监控机制里, 提供了 3 个 IA32_FIXED_CTRx 寄存器用于对特定的事件进行监控统计. 软件可以使用 CPUID.0AH: EDX[4: 0]来确定支持多少个 Fixed 计数器(详见前面 15.2.1 节的相关描述).
Fixed 计数器的监控事件
每个 Fixed 计数器只能监控一个事件, 因此在版本 3 性能监控机制下(目前截止到 SandyBridge 架构)Fixed 事件只有三个, 如下面来自 Intel 手册上的表所示.
[插图]
由于 Fixed 事件对应相应的 Fixed 计数器, 因此, 在使用 Fixed 计数器前无须设置监控事件. 上表中的 Fixed 事件都属于预定义的 architectural 事件(见前面 15.2.4 节描述).
IA32_FIXED_CTR1 与 IA32_FIXED_CTR2 计数器所使用的监控事件, 是统计处理器在 unhalted 状态下的 cycle(时钟周期数), 因此在处理器执行 HALT 指令进入 halted 状态时将不进行计数, 或者在某些情况下, 如 power-saving state 状态、sleep 等状态下, 也不进行计数.
CPU_CLK_UNHALTED.CORE 事件
这个事件将监控 processor core(处理器核心)运行的 clock cycle 数, 如果处理器支持动态加速(调节)频率, 那么这个 CPU_CLK_UNHALTED.CORE 事件的统计频率也随着改变.
CPU_CLK_UNHALTED.REF 事件
这个事件将统计 system bus 的运行 clock cycle 数, 由于 system bus 运行在一个恒定的频率里, 因此这个 CPU_CLK_UNHALTED.REF 事件将统计一个恒定的频率的运行 cycle 值.
Intel 明确提到, CPU_CLK_UNHALTED.REF 事件的计数频率是与 TSC(time stamp counter)一致的. 与 TSC 不同的是, CPU_CLK_UNHALTED.REF 事件可以统计 thread 级(logical processsor)的 clock 数, 而 TSC 只能统计基于 physical processor 级别的 clock 数.
开启 Fixed 计数器
与 IA32_PMC 寄存器的使用情况一样, 除了无须设置监控事件外, 在使用 Fixed 计数器前, 应分别在 IA32_PERF_GLOBAL_CTRL 寄存器与 IA32_FIXED_CTR_CTRL 寄存器里进行开启.
假设, 需要使用 IA32_FIXED_CTR0 计数器, 则可以使用类似下面的代码.

```assembly
; 设置 IA32_FIXED_CTR_CTRL 寄存器
        mov ecx, IA32_FIXED_CTR_CTRL
        mov eax, 0Bh    ;  En=3(all ring levels), ANY=0, PMI=1
        mov edx, 0
        wrmsr
; 开启 IA32_FIXED_CTR0 计数器
        mov ecx, IA32_PERF_GLOBAL_CTRL
        rdmsr
        bts edx, 0     ;  IA32_FIXED_CTR0 enable 位
        wrmsr
```
在 IA32_FIXED_CTR_CTRL 寄存器的 enable 域里设置的值为 3(监控所有 CPL 级别的事件), 如果为 0 值则关闭 Fixed 计数器.

>实验 15-11: 使用 Fixed 计数器

在这个测试实验里, 我们将同时使用 3 个通用计数器和 Fixed 计数器对事件进行统计, 如下表所示.

[插图]

在实验里, 我们的 3 个 IA32_PMC 计数器使用与 Fixed 计数器相同的监控事件, 来对比观察计数器监控的结果.

代码清单 15-29(topic15\ex15-11\protected.asm):

```assembly
; *
; * 实验 ex15-11: 使用 fixed 计数器
; *
        call available_pebs                      ;  测试 pebs 是否可用
        test eax, eax
        jz next                                     ;  不可用
        ; *
        ; * perfmon 初始设置
        ; * 关闭所有 counter 和 PEBS
        ; * 清 overflow 标志位
        ; *
        DISABLE_GLOBAL_COUNTER
        DISABLE_PEBS
        RESET_COUNTER_OVERFLOW
;  设置完整的 DS 区域
        SET_DS_AREA
        ENABLE_BTS_FREEZE_PERFMON_ON_PMI     ;  TR=1, BTS=1
;  设置所有的 fixed 计数器控制器
        mov ecx, IA32_FIXED_CTR_CTRL
        mov eax, 0BBBh                           ;  En=3(all ring levels), ANY=0, PMI=1
        mov edx, 0
        wrmsr
; 设置 IA32_PMC 计数器监控事件
        mov ecx, IA32_PERFEVTSEL0
        mov eax, INST_COUNT_EVENT
        mov edx, 0
        wrmsr
        mov ecx, IA32_PERFEVTSEL1
        mov eax, UNHALTED_CORE_CYCLE_EVENT
        mov edx, 0
        wrmsr
        mov ecx, IA32_PERFEVTSEL2
        mov eax, UNHALTED_REFERENCE_CYCLE_EVENT
        mov edx, 0
        wrmsr
;  同时开启 3 个 IA32_PMCx 计数器, 以及 3 个 IA32_FIXED_CTRx 计数器
        ENABLE_COUNTER 7, 7
;  测试函数
        call test_func
;  关闭所有计数器
        DISABLE_GLOBAL_COUNTER
        DISABLE_BTS_FREEZE_PERFMON_ON_PMI
;  打印 perfmon 单元相关信息
        call dump_perfmon
next:
        jmp $
```

注意在测试代码里, 必须要做到同时开启 counter 和同时关闭 counter, 以免因先后次序而造成 counter 值不同步的结果. 代码在关闭 counter 后, 使用 dump_perfmon()函数来打印 perfmon 相关寄存器的信息.

下面是 Westmere 架构 Core i5 平台上的运行结果.
[插图]

我们看到, 在打印完测试信息后收集的 counter 值, 除了 IA32_PMC0 与 IA32_FIXED_CTR0 两个计数器的值相等外, 其他的计数器的值是不相等的.

这说明, INST_RETIRED.ANY 事件既可用于通用计数器也可用于 Fixed 计数器, 而其余的 CPU_CLK_UNHALTED.CORE 与 CPU_CLK_UNHALTED.REF 事件是为 Fixed 而准备的, 不应使用在通用计数器上.