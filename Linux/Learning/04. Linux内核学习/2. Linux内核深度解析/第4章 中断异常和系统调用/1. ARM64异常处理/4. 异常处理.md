
当处理器取出异常处理的时候, 自动执行下面的操作.

(1) 把当前的处理器状态 (Processor State, PSTATE) 保存在寄存器 SPSR_EL1(保存程序状态寄存器, Saved Program Status Register)中.

(2) 把返回地址保存在寄存器 ELR_EL1(异常链接寄存器, Exception Link Register)中.

* 如果是系统调用, 那么返回地址是系统调用指令后面的指令.

* 如果是除系统调用外的同步异常, 那么返回地址是生成异常的指令, 因为执行完异常处理程序以后需要重新执行生成异常的指令.

* 如果是异步异常, 那么返回地址是没有执行的第一条指令.

(3) 把处理器状态的 DAIF 这 4 个异常掩码位都设置为 1, 禁止这 4 种异常, D 是调试掩码位(Debug mask bit), A 是系统错误掩码位(SError mask bit), I 是中断掩码位(IRQ mask bit), F 是快速中断掩码位(FIQ mask bit)​.

(4) 如果是同步异常或系统错误异常, 把生成异常的原因保存在寄存器 ESR_EL1(异常症状寄存器, Exception Syndrome Register)中.

(5) 如果是同步异常, 把错误地址保存在寄存器 FAR_EL1(错误地址寄存器, Fault Address Register)中.

例如: 访问数据时生成的页错误异常, 错误地址就是数据的虚拟地址; 取指令时生成的页错误异常, 错误地址就是指令的虚拟地址.

(6) 如果处理器处于用户模式(异常级别 0)​, 那么把异常级别提升到 1.

(7) 根据向量基准地址寄存器 VBAR_EL1, 异常类型和生成异常的异常级别计算出异常向量的虚拟地址, 执行异常向量.

对于 64 位应用程序在用户模式 (异常级别 0) 下生成的同步异常, 入口是 el0_sync, 其代码如下:

```
    arch/arm64/kernel/entry.S
    1    el0_sync:
    2     kernel_entry 0
    3     mrs  x25, esr_el1                  // 读异常症状寄存器
    4     lsr  x24, x25, #ESR_ELx_EC_SHIFT   // 异常类别
    5     cmp  x24, #ESR_ELx_EC_SVC64        // 64 位系统调用
    6     b.eq el0_svc
    7     cmp  x24, #ESR_ELx_EC_DABT_LOW     // 异常级别 0 的数据中止
    8     b.eq el0_da
    9     cmp  x24, #ESR_ELx_EC_IABT_LOW     // 异常级别 0 的指令中止
    10    b.eq el0_ia
    11    cmp  x24, #ESR_ELx_EC_FP_ASIMD     // 访问浮点或者高级 SIMD
    12    b.eq el0_fpsimd_acc
    13    cmp  x24, #ESR_ELx_EC_FP_EXC64     // 浮点或者高级 SIMD 异常
    14    b.eq el0_fpsimd_exc
    15    cmp  x24, #ESR_ELx_EC_SYS64        // 可配置陷入
    16    b.eq el0_sys
    17    cmp  x24, #ESR_ELx_EC_SP_ALIGN     // 栈对齐异常
    18    b.eq el0_sp_pc
    19    cmp  x24, #ESR_ELx_EC_PC_ALIGN     // 指令地址对齐异常
    20    b.eq el0_sp_pc
    21    cmp  x24, #ESR_ELx_EC_UNKNOWN      // 异常级别 0 的未知异常
    22    b.eq el0_undef
    23    cmp  x24, #ESR_ELx_EC_BREAKPT_LOW // 异常级别 0 的调试异常
    24    b.ge el0_dbg
    25    b    el0_inv
```

第 2 行代码, 把所有通用寄存器的值保存在当前进程的内核栈中.

第 3 行和第 4 行代码, 从寄存器 ESR_EL1 的第 26～31 位得到异常类别.

根据异常类别调用对应的函数.

(1) 第 5 行和第 6 行代码, 系统调用, 调用函数 el0_svc.

(2) 第 7 行和第 8 行代码, 数据中止, 即访问数据时的页错误异常, 调用函数 el0_da.

(3) 第 9 行和第 10 行代码, 指令中止, 即取指令时的页错误异常, 调用函数 el0_ia.

(4) 第 11 行和第 12 行代码, 访问浮点或高级 SIMD, 调用函数 el0_fpsimd_acc.

(5) 第 13 行和第 14 行代码, 浮点或高级 SIMD 异常, 调用函数 el0_fpsimd_exc.

(6) 第 15 行和第 16 行代码, 可配置陷入, 调用函数 el0_sys.

(7) 第 17 行和第 18 行代码, 栈对齐异常, 调用函数 el0_sp_pc.

(8) 第 19 行和第 20 行代码, 指令地址对齐异常, 调用函数 el0_sp_pc.

(9) 第 21 行和第 22 行代码, 未定义指令, 调用函数 el0_undef.

(10) 第 23 行和第 24 行代码, 调试异常, 调用函数 el0_dbg.

(11) 第 25 行代码, 其他同步异常, 调用函数 el0_inv.

对于内核模式 (异常级别 1) 生成的同步异常, 入口是 el1_sync, 其代码如下:

```
    arch/arm64/kernel/entry.S
    1    el1_sync:
    2    kernel_entry 1
    3    mrs  x1, esr_el1                   // 读异常症状寄存器
    4    lsr  x24, x1, #ESR_ELx_EC_SHIFT    // 异常类别
    5    cmp  x24, #ESR_ELx_EC_DABT_CUR     // 异常级别 1 的数据中止
    6    b.eq el1_da
    7    cmp  x24, #ESR_ELx_EC_IABT_CUR     // 异常级别 1 的指令中止
    8    b.eq el1_ia
    9    cmp  x24, #ESR_ELx_EC_SYS64        // 可配置陷入
    10   b.eq el1_undef
    11   cmp  x24, #ESR_ELx_EC_SP_ALIGN     // 栈对齐异常
    12   b.eq el1_sp_pc
    13   cmp  x24, #ESR_ELx_EC_PC_ALIGN     // 指令地址对齐异常
    14   b.eq el1_sp_pc
    15   cmp  x24, #ESR_ELx_EC_UNKNOWN      // 异常级别 1 的未知异常
    16   b.eq el1_undef
    17   cmp  x24, #ESR_ELx_EC_BREAKPT_CUR // 异常级别 1 的调试异常
    18   b.ge el1_dbg
    19   b    el1_inv
```

第 2 行代码, 把所有通用寄存器的值保存在当前进程的内核栈中.

第 3 行和第 4 行代码, 从寄存器 ESR_EL1 的第 26～31 位得到异常类别.

根据异常类别调用对应的函数.

(1) 第 5 行和第 6 行代码, 数据中止, 即访问数据时的页错误异常, 调用函数 el1_da.

(2) 第 7 行和第 8 行代码, 指令中止, 即取指令时的页错误异常, 调用函数 el1_ia.

(3) 第 9 行和第 10 行代码, 可配置陷入, 调用函数 el1_undef.

(4) 第 11 行和第 12 行代码, 栈对齐异常, 调用函数 el1_sp_pc.

(5) 第 13 行和第 14 行代码, 指令地址对齐异常, 调用函数 el1_sp_pc.

(6) 第 15 行和第 16 行代码, 未定义指令, 调用函数 el1_undef.

(7) 第 17 行和第 18 行代码, 调试异常, 调用函数 el1_dbg.

(8) 第 19 行代码, 其他同步异常, 调用函数 el1_inv.

以 64 位应用程序在用户模式 (异常级别 0) 下访问数据时生成的页错误异常为例, 处理函数是 el0_da, 其代码如下:

```
    arch/arm64/kernel/entry.S
    1    el0_da:
    2    mrs   x26, far_el1
    3    enable_dbg_and_irq   // msr   daifclr, #(8 | 2)
    4    …
    5    clear_address_tag x0, x26
    6    mov  x1, x25
    7    mov  x2, sp
    8    bl   do_mem_abort
    9    b    ret_to_user
```

第 2 行代码, 获取数据的虚拟地址, 存放在寄存器 x26 中.

第 3 行代码, 开启调试异常和中断. 第 8 行代码, 调用 C 语言函数 do_mem_abort.

(1) 寄存器 x0 存放第一个参数: 数据的虚拟地址.

(2) 寄存器 x1 存放第二个参数: 寄存器 ESR_EL1 的值, 即生成异常的原因.

(3) 寄存器 x2 存放第三个参数: 保存在内核栈里面的结构体 pt_regs 的起始地址, 结构体 pt_regs 保存所有通用寄存器的值.

第 9 行代码, 恢复当前进程的寄存器, 返回用户模式, 重新执行触发页错误异常的指令.

以在内核模式 (异常级别 1) 下访问数据时生成的页错误异常为例说明, 处理函数是 el1_da, 其代码如下:

```
    arch/arm64/kernel/entry.S
    1    el1_da:
    2     mrs   x3, far_el1
    3     enable_dbg
    4     tbnz   x23, #7, 1f
    5     enable_irq
    6    1:
    7     clear_address_tag x0, x3
    8     mov   x2, sp             // 结构体 pt_regs
    9     bl   do_mem_abort
    10
    11    disable_irq
    12    kernel_exit 1
```

第 2 行代码, 获取数据的虚拟地址, 存放在寄存器 x3 中.

第 3 行代码, 开启调试异常.

第 4 行代码, 如果已经开启中断, 那么不需要执行第 5 行代码.

第 5 行代码, 开启中断.

第 9 行代码, 调用 C 语言函数 do_mem_abort.

(1) 寄存器 x0 存放第一个参数: 数据的虚拟地址.

(2) 寄存器 x1 存放第二个参数: 寄存器 ESR_EL1 的值, 即生成异常的原因.

(3) 寄存器 x2 存放第三个参数: 保存在内核栈里面的结构体 pt_regs 的起始地址, 结构体 pt_regs 保存所有通用寄存器的值.

第 11 行代码, 关闭中断.

第 12 行代码, 恢复当前进程的寄存器, 重新执行触发页错误异常的指令.

页错误异常的处理过程在 3.14 节中已经详细描述.

当异常处理程序执行完的时候, 调用 kernel_exit 返回. kernel_exit 是一个宏, 参数 el 是返回的异常级别, 0 表示返回异常级别 0,1 表示返回异常级别 1, 主要代码如下:

```
    arch/arm64/kernel/entry.S
        .macro  kernel_exit, el
        …
        ldp  x21, x22, [sp, #S_PC]     // 加载保存的寄存器 ELR_EL1 和 SPSR_EL1 的值
        …
        .if  \el == 0                  /* 如果返回用户模式(异常级别 0)*/
        ldr  x23, [sp, #S_SP]
        msr  sp_el0, x23               /* 恢复异常级别 0 的栈指针寄存器 */
        …
        .endif
        msr  elr_el1, x21
        msr  spsr_el1, x22
        ldp  x0, x1, [sp, #16 * 0]
        ldp  x2, x3, [sp, #16 * 1]
        ldp  x4, x5, [sp, #16 * 2]
        ldp  x6, x7, [sp, #16 * 3]
        ldp  x8, x9, [sp, #16 * 4]
        ldp  x10, x11, [sp, #16 * 5]
        ldp  x12, x13, [sp, #16 * 6]
        ldp  x14, x15, [sp, #16 * 7]
        ldp  x16, x17, [sp, #16 * 8]
        ldp  x18, x19, [sp, #16 * 9]
        ldp  x20, x21, [sp, #16 * 10]
        ldp  x22, x23, [sp, #16 * 11]
        ldp  x24, x25, [sp, #16 * 12]
        ldp  x26, x27, [sp, #16 * 13]
        ldp  x28, x29, [sp, #16 * 14]
        ldr  lr, [sp, #S_LR]
        add  sp, sp, #S_FRAME_SIZE
        eret
        .endm
```

首先使用保存在内核栈里面的寄存器值恢复通用寄存器, 然后执行指令 eret 返回, 继续执行被打断的程序.

执行指令 eret 的时候, 处理器自动使用寄存器 SPSR_EL1 保存的值恢复处理器状态, 使用寄存器 ELR_EL1 保存的返回地址恢复程序计数器(Program Counter, PC)​.
