

ARM64 处理器把系统调用划分到同步异常, 在异常级别 1 的异常向量表中, 系统调用的入口有两个:

(1)如果 64 位应用程序执行系统调用指令 svc, 系统调用的入口是 el0_sync.

(2)如果 32 位应用程序执行系统调用指令 svc, 系统调用的入口是 el0_sync_compat.

el0_sync 的代码如下:

```cpp
//arch/arm64/kernel.c
1    .align   6
2   el0_sync:
3    kernel_entry 0
4    mrs   x25, esr_el1                  // 读异常症状寄存器
5    lsr   x24, x25, #ESR_ELx_EC_SHIFT  // 异常类别
6    cmp   x24, #ESR_ELx_EC_SVC64       // 64 位系统调用
7    b.eq el0_svc
8    …
```

第 3 行代码, 把当前进程的寄存器值保存在内核栈中.

第 4 行代码, 读取异常症状寄存器 esr_el1.

第 5 行代码, 解析出异常症状寄存器的异常类别字段.

第 6 行和第 7 行代码, 如果异常类别是系统调用, 跳转到 el0_svc.

el0_svc 负责执行系统调用, 其代码如下:

```cpp
// arch/arm64/kernel.c
1   /*
2    * 这些是系统调用处理程序使用的寄存器,
3    * 允许我们理论上最多传递 7 个参数给一个函数 – x0～x6
4    *
5    * x7 保留, 用于 32 位模式的系统调用号
6    */
7   sc_nr .req x25            // 系统调用的数量
8   scno      .req  x26       // 系统调用号
9   stbl      .req  x27       // 系统调用表的地址
10  tsk       .req  x28       // 当前进程的 thread_info 结构体的地址
11
12  .align   6
13  el0_svc:
14   adrp    stbl, sys_call_table      // 加载系统调用表的地址
15   uxtw    scno, w8         // 寄存器 w8 里面的系统调用号
16   mov     sc_nr, #__NR_syscalls
17  el0_svc_naked:                // 32 位系统调用的入口
18   stp   x0, scno, [sp, #S_ORIG_X0]   // 保存原来的 x0 和系统调用号
19   enable_dbg_and_irq
20   ct_user_exit 1
21
22   ldr   x16, [tsk, #TSK_TI_FLAGS]   // 检查系统调用钩子
23   tst   x16, #_TIF_SYSCALL_WORK
24   b.ne      __sys_trace
25   cmp     scno, sc_nr          // 检查系统调用号是否超过上限
26   b.hs      ni_sys
27   ldr   x16, [stbl, scno, lsl #3]   // 系统调用表表项的地址
28   blr   x16                          // 调用 sys_* 函数
29   b   ret_fast_syscall
30  ni_sys:
31   mov   x0, sp
32   bl   do_ni_syscall
33   b   ret_fast_syscall
34  ENDPROC(el0_svc)
```

第 14 行代码, 把寄存器 x27 设置为系统调用表 sys_call_table 的起始地址.

第 15 行代码, 把寄存器 x26 设置为系统调用号. 64 位进程使用寄存器 x8 传递系统调用号, w8 是寄存器 x8 的 32 位形式.

第 16 行代码, 把寄存器 x25 设置为系统调用的数量, 也就是(最大的系统调用号 + 1)​.

第 18 行代码, 把寄存器 x0 和 x8 的值保存到内核栈中, x0 存放系统调用的第一个参数, x8 存放系统调用号.

第 19 行代码, 开启调试异常和中断.

第 22～24 行代码, 如果使用 ptrace 跟踪系统调用, 跳转到__sys_trace 处理.

第 25 行和第 26 行代码, 如果进程传递的系统调用号等于或大于系统调用的数量, 即大于最大的系统调用号, 那么是非法值, 跳转到 ni_sys 处理错误.

第 27 行代码, 计算出系统调用号对应的表项地址(sys_call_table + 系统调用号 * 8)​, 然后取出处理函数的地址.

第 28 行代码, 调用系统调用号对应的处理函数.

第 29 行代码, 从系统调用返回用户空间.

ret_fast_syscall 从系统调用返回用户空间, 其代码如下:

```cpp
// arch/arm64/kernel.c
1    ret_fast_syscall:
2    disable_irq
3    str   x0, [sp, #S_X0]   /* DEFINE(S_X0, offsetof(struct pt_regs, regs[0])); */
4    ldr   x1, [tsk, #TSK_TI_FLAGS]
5    and   x2, x1, #_TIF_SYSCALL_WORK
6    cbnz    x2, ret_fast_syscall_trace
7    and   x2, x1, #_TIF_WORK_MASK
8    cbnz    x2, work_pending
9    enable_step_tsk x1, x2
10   kernel_exit 0
11  ret_fast_syscall_trace:
12   enable_irq                        // 开启中断
13   b   __sys_trace_return_skipped    // 我们已经保存了 x0
14
15  work_pending:
16   mov   x0, sp                      // 'regs'
17   bl   do_notify_resume
18  #ifdef CONFIG_TRACE_IRQFLAGS
19   bl   trace_hardirqs_on            // 在用户空间执行时开启中断
20  #endif
21   ldr   x1, [tsk, #TSK_TI_FLAGS]    // 重新检查单步执行
22   b   finish_ret_to_user
23
24  ret_to_user:
25   …
26  finish_ret_to_user:
27   enable_step_tsk x1, x2
28   kernel_exit 0
29  ENDPROC(ret_to_user)
```

第 2 行代码, 禁止中断.

第 3 行代码, 寄存器 x0 已经存放了处理函数的返回值, 把保存在内核栈中的寄存器 x0 的值更新为返回值.

第 4～6 行代码, 如果使用 ptrace 跟踪系统调用, 跳转到 ret_fast_syscall_trace 处理.

第 7 行和第 8 行代码, 如果进程的 thread_info.flags 设置了需要重新调度 (_TIF_NEED_RESCHED) 或者有信号需要处理 (_TIF_SIGPENDING) 等标志位, 跳转到 work_pending 处理.

第 9 行代码, 如果使用系统调用 ptrace 设置了软件单步执行, 那么开启单步执行.

第 10 行代码, 使用保存在内核栈中的寄存器值恢复寄存器, 从内核模式返回用户模式.

work_pending 调用函数 do_notify_resume, 函数 do_notify_resume 的代码如下:

```cpp
//     arch/arm64/kernel/signal.c
1   asmlinkage void do_notify_resume(struct pt_regs *regs,
2                     unsigned int thread_flags)
3   {
4    …
5    do {
6         if (thread_flags & _TIF_NEED_RESCHED) {
7               schedule();
8         } else {
9               local_irq_enable();
10
11              if (thread_flags & _TIF_UPROBE)
12                   uprobe_notify_resume(regs);
13
14              if (thread_flags & _TIF_SIGPENDING)
15                   do_signal(regs);
16
17              if (thread_flags & _TIF_NOTIFY_RESUME) {
18                   clear_thread_flag(TIF_NOTIFY_RESUME);
19                   tracehook_notify_resume(regs);
20              }
21
22              if (thread_flags & _TIF_FOREIGN_FPSTATE)
23                   fpsimd_restore_current_state();
24        }
25
26        local_irq_disable();
27        thread_flags = READ_ONCE(current_thread_info()->flags);
28   } while (thread_flags & _TIF_WORK_MASK);
29  }
```

第 6 行和第 7 行代码, 如果当前进程的 thread_info.flags 设置了标志位_TIF_NEED_RESCHED, 那么调度进程.

第 11 行和第 12 行代码, 如果设置了标志位_TIF_UPROBE, 调用函数 `uprobe_notify_resume()` 处理. uprobes(user-space probes, 用户空间探测器)可以在进程的任何指令地址插入探测器, 收集调试和性能信息, 发现性能问题. 需要内核支持, 编译内核时开启配置宏 CONFIG_UPROBE_EVENTS.

第 14 行和第 15 行代码, 如果设置了标志位 `_TIF_SIGPENDING`, 调用函数 `do_signal()` 处理信号.

第 17～20 行代码, 如果设置了标志位_TIF_NOTIFY_RESUME, 那么调用函数 `tracehook_notify_resume()`, 执行返回用户模式之前的回调函数.

第 22 行和第 23 行代码, 如果设置了标志位_TIF_FOREIGN_FPSTATE, 那么恢复浮点寄存器.
