
从标号 reset 开始的代码如下:

```
arch/arm/cpu/armv8/start.S
1 reset:
2   /* 允许板卡保存重要的寄存器 */
3   b save_boot_params
4 .globl save_boot_params_ret
5 save_boot_params_ret:
6
7 #ifdef CONFIG_SYS_RESET_SCTRL
8   bl reset_sctrl
9 #endif
10  /*
11   * 异常级别可能是 3, 2 或者 1, 初始状态:
12   * 小端字节序, 禁止 MMU, 禁止指令 / 数据缓存
13   */
14   adr x0, vectors
15   switch_el x1, 3f, 2f, 1f
16  3: msr vbar_el3, x0
17   mrs x0, scr_el3
18   orr x0, x0, #0xf /* 设置寄存器 SCR_EL3 的 NS, IRQ, FIQ 和 EA 四个位 */
19   msr scr_el3, x0
20   msr cptr_el3, xzr /* 启用浮点和 SIMD 功能 */
21  #ifdef COUNTER_FREQUENCY
22   ldr x0, =COUNTER_FREQUENCY
23   msr cntfrq_el0, x0 /* 初始化寄存器 CNTFRQ */
24  #endif
25   b 0f
26  2: msr vbar_el2, x0
27   mov x0, #0x33ff
28   msr cptr_el2, x0 /* 启用浮点和 SIMD 功能 */
29   b 0f
30  1: msr vbar_el1, x0
31   mov x0, #3 << 20
32   msr cpacr_el1, x0 /* 启用浮点和 SIMD 功能 */
33  0:
34  …
35
36   /* 应用 ARM 处理器特定的勘误表 */
37   bl apply_core_errata
38
39   /* 处理器特定的初始化 */
40   bl lowlevel_init
41
42  #if defined(CONFIG_ARMV8_SPIN_TABLE) && !defined(CONFIG_SPL_BUILD)
43   branch_if_master x0, x1, master_cpu // 主处理器跳转到 master_cpu
44   b spin_table_secondary_jump
45   /* 绝对不会返回 */
46  #elif defined(CONFIG_ARMV8_MULTIENTRY)
47   branch_if_master x0, x1, master_cpu
48
49   /*
50    * 从处理器
51    */
52  slave_cpu:
53   wfe
54   ldr x1, =CPU_RELEASE_ADDR
55   ldr x0, [x1]
56   cbz x0, slave_cpu
57   br x0 /* 跳转到指定地址 */
58  #endif /* CONFIG_ARMV8_MULTIENTRY */
59  master_cpu:
60   bl _main // 主处理器执行 _main
```

第 3 行代码, 调用各种**板卡自定义**的函数 `save_boot_params` 来保存重要的寄存器.

第 8 行代码, 调用函数 `reset_sctrl` 来初始化**系统控制寄存器**. 由配置宏 `CONFIG_SYS_RESET_SCTRL` 控制, 一般**不需要打开**.

> 避免早期固件/loader 等设置的旧的映射.

第 15～32 行代码, 根据处理器**当前的异常级别**设置寄存器.

* 第 16～24 行代码, 如果异常级别是 3, 那么把**向量基准地址寄存器** (`VBAR_EL3`) 设置为**异常向量表的起始地址**; 设置**安全配置寄存器** (`SCR_EL3`) 的 (NS, IRQ, FIQ 和 EA) 这 4 个位, 也就是异常级别 0, 1 和 2 处于**非安全状态**, 在任何异常级别执行时都把(**中断**, **快速中断**, **同步外部中止**和**系统错误**)转发到异常级别 3; 把**协处理器陷入寄存器** (`CPTR_EL3`) 设置为 0, 允许 EL0/EL1/EL2 访问**浮点**(`CPTR_EL3.ESM`)和**单指令多数据** (Single Instruction Multiple Data, SIMD, `CPTR_EL3.TFP`) 功能, 否则会陷入 EL3; 设置**计数器时钟频率寄存器**(`CNTFRQ_EL0`).

* 第 26～28 行代码, 如果异常级别是 2, 那么把**向量基准地址寄存器** (`VBAR_EL2`) 设置为**异常向量表的起始地址**; 设置**协处理器陷入寄存器**(`CPTR_EL2.TFP`), 允许 EL0/EL1 访问浮点和 SIMD 功能(否则 EL0/EL1 访问会陷入到 EL2).

* 第 30～32 行代码, 如果异常级别是 1, 那么把**向量基准地址寄存器** (`VBAR_EL1`) 设
置为**异常向量表的起始地址**; 设置**协处理器访问控制寄存器**(`CPACR_EL1.FPEN`), 允
许 EL0/EL1 访问浮点和 SIMD 功能(否则触发异常).

第 37 行代码, 为处理器的缺陷打补丁.

第 40 行代码, 调用函数 `lowlevel_init` 以执行函数 `board_init_f()` 所需要的最小初始化. 当前文件定义了**弱符号类型**的函数 `lowlevel_init`, 处理器厂商可以自定义强符号类型的函数 `lowlevel_init` 以覆盖弱符号.

第 42～58 行代码, 如果是**多处理器系统**, 那么**只有一个处理器**是**主处理器**(也称为引导处理器), 其他处理器是从处理器.

* 第 42～44 行代码, 如果使用**自旋表启动方法**, 并且**不是**编译为**第二程序加载器**, 那么**从处理器**执行函数 `spin_table_secondary_jump`(`arch/arm/cpu/armv8/spin_table_v8.S`), 执行过程为: **从处理器**进入**低功耗状态**, 它被唤醒的时候, 从地址 `spin_table_cpu_release_addr` 读取函数地址, 如果**主处理器**还没有指定函数地址, 继续等待; 如果主处理器指定了函数地址, 就跳转到指定的函数地址执行.

    ```
    ENTRY(spin_table_secondary_jump)
    .globl spin_table_reserve_begin
    spin_table_reserve_begin:
    0:	wfe // 进入低功耗状态
        ldr	x0, spin_table_cpu_release_addr
        cbz	x0, 0b // x0 是 0, 跳转到 0: 处(继续等待)
        br	x0 // 跳转到指定的函数地址执行
    .globl spin_table_cpu_release_addr
        .align	3
    spin_table_cpu_release_addr:
        .quad	0
    .globl spin_table_reserve_end
    spin_table_reserve_end:
    ENDPROC(spin_table_secondary_jump)
    ```

* 第 46～57 行代码, 如果**允许多个处理器进入引导程序**, 那么**从处理器**进入**低功耗状态**, 它被唤醒的时候, 从地址 `CPU_RELEASE_ADDR` 读取函数地址, 如果主处理器还没有指定函数地址, 继续等待; 如果主处理器指定了函数地址, 就跳转到指定的函数地址执行.

第 60 行代码, 主处理器执行函数 `_main`.

下面介绍第二阶段程序加载器.

U-Boot 分为 SPL 和正常的 U-Boot 程序两个部分, 如果想要编译为 SPL, 需要开启配置宏 `CONFIG_SPL_BUILD`. SPL 是 "Secondary Program Loader" 的简称, 即第二阶段程序加载器, 第二阶段是相对于**处理器里面**的**只读存储器中的固化程序**来说的, 处理器启动时最先执行的是只读存储器中的固化程序.

**固化程序**通过**检测启动方式**来加载第二阶段程序加载器. 为什么需要第二阶段程序加载器? 原因是: 一些处理器内部集成的**静态随机访问存储器比较小**, 无法装载一个完整的 U-Boot 镜像, 此时需要第二阶段程序加载器, 它主要负责**初始化内存**和**存储设备驱动**, 然后把正常的 **U-Boot 镜像**从**存储设备**读到**内存**中执行.

# 小结

所有 CPU 开始执行

1) 保存寄存器(定义来自板卡)

2) 配置 向量基准地址寄存器(`VBAR_EL3`/`VBAR_EL2`/`VBAR_EL1`)为 异常向量表的起始地址

3) 处于不同 EL 级别:

* EL3. **安全配置寄存器** (`SCR_EL3`), 让 EL0/EL1/EL2 都在 non-secure 模式下运行, 所有异常全部路由到 EL3; 设置 CPTR_EL3, 允许 EL0/EL1/EL2 访问**浮点**和**单指令多数据**; 设置**计数器时钟频率寄存器**

* EL2/EL1. 设置 CPTR_EL2/CPACR_EL1, 允许上层访问浮点和 SIMD 功能;

4) 处理器特定初始化(不同处理器不同)

5) 不同处理器

* **主处理器**去执行 `_main`

* **从处理器**进入低功耗状态(WFE)

  * 自旋表启动, 自旋获取 spin_table_cpu_release_addr

  * 允许多个处理器进入引导程序, 自旋获取 CPU_RELEASE_ADDR