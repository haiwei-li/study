
函数 `_main` 的代码如下:

```
// arch/arm/lib/crt0_64.S
1 ENTRY(_main)
2
3 /*
4  * 设置初始的 C 语言运行环境, 并且调用 board_init_f(0).
5  */
6 #if defined(CONFIG_SPL_BUILD) && defined(CONFIG_SPL_STACK
7  ldr x0, =(CONFIG_SPL_STACK)
8 #else
9  ldr x0, =(CONFIG_SYS_INIT_SP_ADDR)
10 #endif
11  bic sp, x0, #0xf /* 为了符合应用二进制接口规范, 对齐到 16 字节 */
12  mov x0, sp
13  bl board_init_f_alloc_reserve
14  mov sp, x0
15  /* 设置 gd */
16  mov x18, x0
17  bl board_init_f_init_reserve
18
19  mov x0, #0
20  bl board_init_f
21
22  #if !defined(CONFIG_SPL_BUILD)
23  /*
24   * 设置中间环境 (新的栈指针和 gd), 然后调用函数
25   * relocate_code(addr_moni).
26   *
27   */
28   ldr x0, [x18, #GD_START_ADDR_SP] /* 把寄存器 x0 设置为 gd->start_addr_sp */
29   bic sp, x0, #0xf /* 为了符合应用二进制接口规范, 对齐到 16 字节 */
30   ldr x18, [x18, #GD_BD] /* 把寄存器 x18 设置为 gd->bd */
31   sub x18, x18, #GD_SIZE /* 新的 gd 在 bd 的下面 */
32
33   adr lr, relocation_return
34   ldr x9, [x18, #GD_RELOC_OFF] /* 把寄存器 x9 设置为 gd->reloc_off */
35   add lr, lr, x9 /* 在重定位后新的返回地址 */
36   ldr x0, [x18, #GD_RELOCADDR] /* 把寄存器 x0 设置为 gd->relocaddr */
37   b relocate_code
38
39 relocation_return:
40
41 /*
42  * 设置最终的完整环境
43  */
44  bl c_runtime_cpu_setup /* 仍然调用旧的例程 */
45 #endif /* !CONFIG_SPL_BUILD */
46 #if defined(CONFIG_SPL_BUILD)
47  bl spl_relocate_stack_gd /* 可能返回空指针 */
48  /*
49   * 执行 "sp = (x0 != NULL) ? x0 : sp",
50   * 规避这个约束:
51   * 带条件的 mov 指令不能把栈指针寄存器作为操作数
52   */
53  mov x1, sp
54  cmp x0, #0
55  csel x0, x0, x1, ne
56  mov sp, x0
57 #endif
58
59 /*
60  * 用 0 初始化未初始化数据段
61  */
62  ldr x0, =__bss_start /* 这是自动重定位 */
63  ldr x1, =__bss_end /* 这是自动重定位 */
64  clear_loop:
65  str xzr, [x0], #8
66  cmp x0, x1
67  b.lo clear_loop
68
69 /* 调用函数 board_init_r(gd_t *id, ulong dest_addr) */
70 mov x0, x18 /* gd_t */
71 ldr x1, [x18, #GD_RELOCADDR] /* dest_addr */
72 b board_init_r /* 相对程序计数器的跳转 */
73
74 /* 不会运行到这里, 因为函数 board_init_r() 不会返回 */
75
76 ENDPROC(_main)
```

第 6～17 行代码, 设置 C 代码的运行环境, 为调用函数 `board_init_f` 做准备.

* 第 11 行代码, 设置临时的栈.

* 第 13 行代码, 调用函数 `board_init_f_alloc_reserve`, 在栈的顶部为结构体 `global_data` 分配空间.

* 第 17 行代码, 调用函数 `board_init_f_init_reserve`, 初始化结构体 `global_data`.

第 20 行代码, 调用函数 `board_init_f`(f 是 front, 表示前期), 执行 **前期初始化**. 为了 **把 U-Boot 程序复制到内存** 中来执行, **初始化硬件**, 做准备工作. 文件 "`common/board_f.c`" 定义了公共的函数 `board_init_f`, 函数 `board_init_f` **依次执行** 数组 `init_sequence_f` 中的每个函数.

```cpp
// common/board_f.c
void board_init_f(ulong boot_flags)
{
    ...
    if (initcall_run_list(init_sequence_f))
        hang();
    ...
}

static const init_fnc_t init_sequence_f[] = {
	setup_mon_len,
#ifdef CONFIG_OF_CONTROL
	fdtdec_setup,
#endif
#ifdef CONFIG_TRACE_EARLY
	trace_early_init,
#endif
	initf_malloc,
	initf_upl,
    ...
	env_init,		/* initialize environment */
	init_baud_rate,		/* initialze baudrate settings */
	serial_init,		/* serial communications setup */
	console_init_f,		/* stage 1 init of console */
	display_options,	/* say that we are here */
	display_text_info,	/* show debugging info if required */
    ...
}

display_text_info 打印: U-Boot code: 00200000 -> 0032A2A0  BSS: -> 004B2D80
```

第 22～45 行代码, 如果编译为正常的引导程序, 那么调用函数 `relocate_code`, 把 U-Boot 程序复制到内存中, 重新定位, 然后调用函数 `c_runtime_cpu_setup`, 把 **向量基准地址寄存器** 设置为 **异常向量表** 的 **起始地址**. 这里是分界线, 以前处理器从 NOR 闪存取指令, 这一步执行完以后处理器从内存取指令.

第 46～ 57 行代码, 如果编译为 **第二阶段程序加载器**, 那么调用函数 `spl_relocate_stack_gd` 重新定位栈.

第 62～67 行代码, 用 0 初始化未初始化数据段.

第 72 行代码, 调用函数 `board_init_r`( r 是 rear, 表示后期), 执行后期初始化. 文件 "`common/board_r.c`" 定义了函数 board_init_r, 依次执行数组 `init_sequence_r` 中的每个函数, 最后一个函数是 `run_main_loop`.

# 小结

1) 为调用 board_init_f() 设置初始环境. 此环境仅提供一个栈和一个用于存储 GD("全局数据")结构的位置, 二者都位于某些易于访问的 RAM(SRAM, 锁定缓存等)中. 在此上下文中, 变量全局数据, 无论是已初始化的还是未初始化的(BSS), 都是不可用的; 只有已初始化的常量数据是可用的. 在调用 board_init_f() 之前, 应将 GD 清零.

2) 调用 board_init_f(). 此函数为从系统 RAM(DRAM,DDR 等)执行做好硬件准备. 由于系统 RAM 可能尚未可用, board_init_f() 必须使用当前的 GD 来存储必须传递给后续阶段的任何数据. 这些数据包括重定位目标, 未来的栈以及未来的 GD 位置.

3) 设置中间环境, 其中栈和 GD 是由 board_init_f() 在系统 RAM 中分配的, 但 BSS 和已初始化的非常量数据仍然不可用.

4a) 对于 U-Boot(非 SPL), 调用 relocate_code(). 此函数将 U-Boot 从其当前位置重定位到由 board_init_f() 计算出的重定位目标位置.

4b) 对于 SPL, board_init_f() 只是返回(到 crt0). 在 SPL 中没有代码重定位.

5) 为调用 board_init_r() 设置最终环境. 此环境具有 BSS(初始化为 0), 已初始化的非常量数据 (初始化为其预期值) 以及位于系统 RAM 中的栈(对于 SPL, 将栈和 GD 移动到 RAM 是可选的 —— 参见 CONFIG_SPL_STACK_R). GD 保留了由 board_init_f() 设置的值.

主 CPU 的操作

*
* 复制 u-boot 程序到内存
* 初始化硬件
* 核心都在 `init_sequence_f` 函数链表
