
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. IA32_FEATURE_CONTROL MSR 寄存器](#1-ia32_feature_control-msr-寄存器)
  - [1.1. 小结](#11-小结)
- [2. CR0 与 CR4 固定位](#2-cr0-与-cr4-固定位)
  - [2.1. 小结](#21-小结)
- [3. A20M 模式](#3-a20m-模式)
- [](#)
- [](#-1)

<!-- /code_chunk_output -->

**执行 VMXON 指令**的基本要求是:

* 需要开启`CR4.VMXE`位, 不能在**实模式**, `virtual-8086`以及**兼容模式**下进行, 否则将产生`#UD`异常;

* **不能**在**非 0**级权限下执行, 否则将产生`#GP`异常.

# 1. IA32_FEATURE_CONTROL MSR 寄存器

`IA32_FEATURE_CONTROL MSR` 寄存器(index `3AH`)也**影响着 VMXON 指令的执行**. 当**物理处理器**被**reset**, 这个 MSR 会全部被**清零**.

bit 1 与 bit 2 分别是 **SMX** 模式的 **inside** 与 **outside** 位. inside 位指示允许在 SMX 内使用 VMX, outside 位指示允许在 SMX 外使用 VMX(这是开启 VMX 模式最基本的条件).

(1) **lock 位**(`bit 0`).

* 当 lock 位为 0 时, **执行 VMXON 指令**将产生 `#GP` 异常.

* 当 lock 位为 1 时, 即上锁后, 对 `IA32_FEATURE_CONTROL` 寄存器进行**写操作**, 将产生 `#GP` 异常. 直到发生**上电复位**, 否则不能修改这个 MSR.

因此, **执行 VMXON 指令前**必须确保 lock 位为 **1** 值, 也就是锁上 `IA32_FEATURE_CONTROL` 寄存器.

**系统 BIOS！！！** 可以**使用此 bit** 为 BIOS **提供设置选项**, 以**禁用对 VMX 的支持**. 为了启用 VMX 支持, BIOS 必须设置 `bit 1` 或 `bit 2` 或**两者均设置**, 当然 bit 0 必须设置.

(2) Enable VMX **inside** SMX 位(`bit 1`).

* 当为 1 时, 表明当处理器处于 `SMX` (**Safer Mode Extensions**)模式**时**, **允许开启 VMX operation 模式**(即调用 `VMXON` 指令).
* 当 为 0 时, 当处理器处于 `SMX` 模式时, 执行 VMXON 指令将引发 `#GP` 异常.
* 当 cpu**不支持 VMX** 架构(见`2.2.3`), 或者**不支持 SMX 模式**, 在这个 cpu 上设置这个 bit 会引发`#GP`. (not both 语法. Attempts to set this bit on logical processors that do not support both VMX and SMX operation cause general-protection exceptions). 即**只有同时**支持 VMX 与 SMX 模式时(即 `CPUID.01H:ECX[6:5]=11B`), 允许对 bit 1(in) 置位

(3) Enable VMX **outside** SMX 位(`bit 2`)

* 当为 1 时, 表明当处理器在 **SMX 模式之外**时, 允许开启 VMX operation 模式(即调用 `VMXON` 指令).

* 当为 0 时, 表明当处理器**不在 SMX 模式**时, 执行 VMXON 指令将引发 `#GP` 异常.

* 尝试在**不支持 VMX 架构**(见`2.2.3`)的 CPU 上设置此位会导致 `#GP`. 即在**支持 VMX 模式**时(即 `CPUID.01H:ECX[5]=1`), **才允许**对 bit 2(out) 置位.

注意: "`Enable VMX outside SMX`"(`bit 2`)位需要**置位**, 这是运行 VMX 模式的**基本条件**. `bit 1` 与 `bit 2` 允许同时置位, 此时表示无论是 SMX 模式内还是模式外都可以开启 VMX 模式.

下面的代码片段来自 `lib\system_data_manage.asm` 里的 `get_vmx_global_data` 函数, 用来收集和分析 VMX 的数据:

```x86asm
get_vmx_global_data.@6:
        ......
        ;;
        ;; 关于 IA32_FEATURE_CONTROL.lock 位:
        ;; 1) 当 lock = 0 时, 执行 VMXON 产生 #GP 异常
        ;; 2) 当 lock = 1 时, 写 IA32_FEATURE_CONTROL 寄存器产生 #GP 异常
        ;;

        ;;
        ;; 下面将检查 IA32_FEATURE_CONTROL 寄存器
        ;; 1) 当 lock 位为 0 时, 需要进行一些设置, 然后锁上 IA32_FEATURE_CONTROL
        ;;
        mov ecx, IA32_FEATURE_CONTROL
        rdmsr
        bts eax, 0                                                      ; 检查 lock 位, 并上锁
        jc get_vmx_global_data.@7

        ;; lock 未上锁时:
        ;; 1) 对 lock 置位(锁上 IA32_FEATURE_CONTROL 寄存器)
        ;; 2) 对 bit 2 置位(启用 enable VMXON outside SMX)
        ;; 3) 如果支持 enable VMXON inside SMX 时, 对 bit 1 置位!
        ;;
        mov esi, 6                                                      ; enable VMX outside SMX = 1, enable VMX inside SMX = 1
        mov edi, 4                                                      ; enable VMX outside SMX = 1, enable VMX inside SMX = 0

        ;;
        ;; 检查是否支持 SMX 模式
        ;;
        test DWORD [gs: PCB.FeatureEcx], CPU_FLAGS_SMX
        cmovz esi, edi
        or eax, esi
        wrmsr


get_vmx_global_data.@7:

        ;;
        ;; 假如使用 enable VMX inside SMX 功能, 则根据 IA32_FEATURE_CONTROL[1] 来决定是否必须开启 CR4.SMXE
        ;; 1) 本书例子中没有开启 CR4.SMXE
        ;;
%ifdef ENABLE_VMX_INSIDE_SMX
        ;;
        ;; ### step 7: 设置 Cr4FixedMask 的 CR4.SMXE 位 ###
        ;;
        ;; 再次读取 IA32_FEATURE_CONTROL 寄存器
        ;; 1) 检查 enable VMX inside SMX 位(bit1)
        ;;    1.1) 如果是 inside SMX(即 bit1 = 1), 则设置 CR4FixedMask 位的相应位
        ;;
        rdmsr
        and eax, 2                                                      ; 取 enable VMX inside SMX 位的值(bit1)
        shl eax, 13                                                     ; 对应在 CR4 寄存器的 bit 14 位(即 CR4.SMXE 位)
        or DWORD [ebp + PCB.Cr4FixedMask], eax                          ; 在 Cr4FixedMask 里设置 enable VMX inside SMX 位的值　

%endif
```

这段代码检查 `IA32_FEATURE_CONTROL` 寄存器的 lock 位是否为 1. **不为 1 时**, 需要**上锁**, 并对"`Enable VMX outside SMX`"**置位**, 同时根据 `CPUID.01H:EDX[6].SMX` 位来设置"`Enable VMX inside SMX`"位.

在定义了 `ENABLE_VMX_INSIDE_SMX` 符号时, 接下来分析是否启用"Enable VMX inside SMX", 如果是, 那么 `Cr4FixedMask` 的值需要添加 `CR4.SMXE` 位(需要为 1 值), 也就是必须要开启 SMX 模式. 本书中没有开启 CR4.SMXE 位.

## 1.1. 小结

其实就是总结上面的代码

总结起来, 在 vmxon 指令之前, **必须支持 VMX 架构**, 而对于`IA32_FEATURE_CONTROL MSR` 寄存器, **手动设置**如下:

* lock 位(`bit 0`)为 1

* outside SMX 位(`bit 2`)为 1

* inside SMX 位: 如果开启了 tboot, 那就设置为 1; 否则为 0

而最后`IA32_FEATURE_CONTROL MSR` 寄存器只要**同时符合**下面条件即可:

* SMX 启用的话, in 必须等于 1

* SMX 没有启用, out 必须等于 1

# 2. CR0 与 CR4 固定位

**进入 VMX operation 模式前**, **CR0** 与 **CR4** 寄存器也**必须**要满足 VMX operation 模式运行的**条件**:

(1) **CR0** 寄存器需要满足 **PG**(`bit 31`)、**NE**(`bit 5`), 以及 **PE**(`bit 0`) 位为 1. 也就是需要开启**分页机制**的**保护模式**(包括了 `IA-32e` 模式), x87 FPU 数字异常的**处理模式**需要使用 **native 模式**.

(2) **CR4** 寄存器需要开启 **VMXE 位**(`bit 13`).

这些位在 `IA32_VMX_CR0_FIXED0` MSR 寄存器里属于 `Fixed to 1`(**固定为 1**) 位(参见第 `2.5.10` 节), 否则执行 VMXON 指令将会产生 `#GP` 异常. 由于 `CR0.PG` 与 `CR0.PE` 位必须为 1, 因此 VMX operation **不能**从**实模式**和**非分页**的**保护模式**里进入. 如果需要在 **SMX 模式**里进入 VMX 模式, 那么 `CR0.SMXE` 位(`bit 14`) 也需要为 **1** 值.

如果这些 bits 有任何不支持的值(可以见`2.6.6 VMX 模式管理指令`中 VMXON 部分), VMXON 会失败. 在 VMX operation 模式(包括 VMX root operation)中, 尝试写(包括`CLTS`、`LMSW`或 `MOV CR`指令等)不支持的值到这些 bits 会产生`#GP`异常. VM-entry 或 VM-exit 也不能设置成不支持的值.

软件应该参考`IA32_VMX_CR0_FIXED0`和`IA32_VMX_CR0_FIXED1`来决定 CR0 中哪些位是固定的, CR4 同理. (参见第 `2.5.10` 节`).

在 **VMCS 区城的字段**设置里, 也有很多**类似**这样的**制约条件**存在, 某些位必须为 1 值, 某些位必须为 0 值, 某些位可以为 0 值也可以为 1 值. 例如, 上面的 CR0 与 CR4 寄存器列举的位必须为 1 值. 因此, 软件需要检测制约条件而进行相应的设置, 这些设置也可能随着**处理器架构的发展**而**有所改变**.

上面所述的那些 CR0 与 CR4 寄存器**固定位**从**第 1 代支持 VMX 的处理器**开始就被确定了, 后面我们将会看到软件需要使用 `IA32_VMX_CR0_FIXED0 MSR` 与 `IA32_VMX_CR0_FIXED1 MSR` 对 **CR0 寄存器**的**固定位**进行检测, 使用 `IA32_VMX_CR4_FIXED0 MSR` 与 `IA32_VMX_CR4_FIXED1 MSR` 对 **CR4** 寄存器**固定位**进行检测.

* `IA32_VMX_CR0_FIXED0 MSR`: 486H
* `IA32_VMX_CR0_FIXED1 MSR`: 487H
* `IA32_VMX_CR4_FIXED0 MSR`: 488H
* `IA32_VMX_CR4_FIXED1 MSR`: 489H

下面一段代码来自 `libl\Vmx\VmxInit.asm` 里的 `initialize_vmxon_region` 函数, 执行对 **CR0** 与 **CR4** 寄存器**固定位**的设置.

```x86asm
        ;;
        ;; 读 CR0 当前值
        ;;
        REX.Wrxb
        mov ecx, cr0
        mov ebx, ecx

        ;;
        ;; 检查 CR0.PE 与 CR0.PG 是否符合 fixed 位, 这里只检查低 32 位值
        ;; 1) 对比 Cr0FixedMask 值(固定为 1 值), 不相同则返回错误码
        ;;
        mov eax, STATUS_VMX_UNEXPECT                    ; 错误码(超出期望值)
        xor ecx, [ebp + PCB.Cr0FixedMask]               ; 与 Cr0FixedMask 值异或, 检查是否相同
        js initialize_vmxon_region.done                 ; 检查 CR0.PG 位是否相等
        test ecx, 1
        jnz initialize_vmxon_region.done                ; 检查 CR0.PE 位是否相等

        ;;
        ;; 如果 CR0.PE 与 CR0.PG 位相符, 设置 CR0 其它位
        ;;
        or ebx, [ebp + PCB.Cr0Fixed0]                   ; 设置 Fixed 1 位
        and ebx, [ebp + PCB.Cr0Fixed1]                  ; 设置 Fixed 0 位
        REX.Wrxb
        mov cr0, ebx                                    ; 写回 CR0

        ;;
        ;; 直接设置 CR4 fixed 1 位
        ;;
        REX.W
        mov ecx, cr4
        or ecx, [ebp + PCB.Cr4FixedMask]                ; 设置 Fixed 1 位
        and ecx, [ebp + PCB.Cr4Fixed1]                  ; 设置 Fixed 0 位
        REX.W
        mov cr4, ecx
```

代码设置流程如下:

(1) 检查 **CR0** 寄存器的 **PE** 与 **PG** 位是否满足条件(在**开启分页的保护模式**下), 通过与 CR0fixedmask 值进行比较, 如果 PG 和 PE 位与 CR0fixedmask 中的 PG 和 PE 值不同, 则失败返回. 此错误不能在当前函数修复.

(2) 如果 CR0 满足上面的基本条件, 则直接设置 CR0 的 Fixed to 1(固定 1) 位与  Fixed to 0(固定 0) 位. 这里的 **Cr0fixed0** 与 **Cr0fixed1** 的值来自于 `IA32_VMX_CR0_FIXEDO` 与 `IA32_VMX_CR0_FIXED1` 寄存器.

(3) CR4 可以直接设置, 通过"或"上 Cr4FixedMask 值, 然后"与"上 Cr4Fixed1 值, 得到最终的 CR4 的值.

CR0 与 CR4 寄存器设置的算法是一样的, 通过 "**或**" **固定为 1 值**, 然后 "**与**" **固定为 0 值**, 得出最后需要的值.

根据 CR0 寄存器的限制, 处理器在 VMX operation 模式(包括 `VMX root-operation` 与 `VMX non-root operation` 模式)必须要开启**分页保护模式**, 那么 guest 软件必须运行在分页保护模式, 在进入 guest 端时, 处理器会检查是否符合这个要求. 当不符合这个制约条件时, 将产生 VM-entry 失败. 如果需要 guest 软件执行实模式代码, 那么可以选择进  virtual-8086 模式.

后续的处理器可能会支持在 VMX non-root operation 模式下使用**实模式**或**未启用分页的保护模式**, 软件需要检测是否支持"`unrestricted guest`"(不受限制的 guest)功能. 在支持并开启 "**unrestricted guest**" 功能后, guest 软件允许运行在**实模式**或者**未分页的保护模式**.

在这种情况下, 进入 guest 时, 处理器将不会检查 `CR0.PE` 与 `CR0.PG` 控制位(参见第 4.5.1 节), 而忽略 CR0 的 Fixed to 1(固定为 1) 中的相应位. 但是, 如果 `CR0.PG` 为 1, 则 `CR0.PE` 必须为 1.

## 2.1. 小结



# 3. A20M 模式

还有一个制约就是: 当处理器处于 **A20M 模式**(即 **A20 线 mask 模式**), 也**不允许**进入 **VMX operation** 模式, 执行 VMXON 指令将引发 `#GP` 异常. A20M 模式会**屏蔽 A20 地址线**产生所谓的"wrapping"现象, 从而模拟 8086 处理器的 1M 地址内访问行为

例子的 boot 阶段就调用了 `FAST_A20_ENABLE` 宏来开启 A20 地址线(即 A20 线  unmask), 这个宏实现在 `inc\cpu.inc` 文件里

#

The INIT signal is blocked whenever a logical processor is in VMX root operation. It is not blocked in VMX non- root operation. Instead, INITs cause VM exits (see Section 25.2, "Other Causes of VM Exits").

#

• Intel® Processor Trace (Intel PT) can be used in VMX operation only if IA32_VMX_MISC[14] is read as 1 (see Appendix A.6). On processors that support Intel PT but which do not allow it to be used in VMX operation, execution of VMXON clears IA32_RTIT_CTL.TraceEn (see "VMXON—Enter VMX Operation" in Chapter 30); any attempt to write IA32_RTIT_CTL while in VMX operation (including VMX root operation) causes a general- protection exception.