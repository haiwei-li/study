

启用处理器虚拟化机制前, 必须先开启 VMX operation 模式的允许(开启 `CR4.VMXE` 位), 表明**允许执行 VMXON 指令进入 VMX operation 模式**.

在执行 **VMXON 指令**切换到 VMX operation 模式**之前**, 还需要做一系列的**准备**工作, 下面的代码片段摘自 `lib/vmx/vmxinit.asm` 文件里的 `vmx_operation_enter` 函数, 这里简要地了解一下.

```x86asm
;----------------------------------------------------------
; vmx_operation_enter()
; input:
;       esi - VMXON region pointer
; output:
;       0 - successful
;       otherwise - 错误码
; 描述:
;       1) 使处理器进入 VMX root operation 环境
;----------------------------------------------------------
vmx_operation_enter:
        push ecx
        push edx
        push ebp



%ifdef __X64
        LoadGsBaseToRbp
%else
        mov ebp, [gs: PCB.Base]
%endif

        mov eax, STATUS_SUCCESS

        ;;
        ;; 检查是否已经进入了 VMX root operation 模式
        ;;
        test DWORD [ebp + PCB.ProcessorStatus], CPU_STATUS_VMXON
        jnz vmx_operation_enter.done

        ;;
        ;; 检测是否支持 VMX
        ;;
        bt DWORD [ebp + PCB.FeatureEcx], 5
        mov eax, STATUS_UNSUCCESS
        jnc vmx_operation_enter.done

        ;;
        ;; 开启 VMX operation 允许
        ;;
        REX.Wrxb
        mov eax, cr4
        REX.Wrxb
        bts eax, 13                                     ; CR4.VMEX = 1
        REX.Wrxb
        mov cr4, eax

        ;;
        ;; 更新指令状态, 允许执行 VMX 指令
        ;;
        or DWORD [ebp + PCB.InstructionStatus], INST_STATUS_VMX

        ;;
        ;; 初始化 VMXON 区域
        ;;
        call initialize_vmxon_region
        cmp eax, STATUS_SUCCESS
        jne vmx_operation_enter.done

        ;;
        ;; 进入 VMX root operation 模式
        ;; 1) operand 是物理地址 pointer
        ;;
        vmxon [ebp + PCB.VmxonPhysicalPointer]

        ;;
        ;; 检查 VMXON 指令是否执行成功
        ;; 1) 当 CF = 0 时, WMXON 执行成功
        ;; 1) 当 CF = 1 时, 返回失败
        ;;
        mov eax, STATUS_UNSUCCESS
        jc vmx_operation_enter.done
        jz vmx_operation_enter.done

        ......
vmx_operation_enter.@0:

        ;;
        ;; 更新处理器状态
        ;;
        or DWORD [ebp + PCB.ProcessorStatus], CPU_STATUS_VMXON

        mov eax, STATUS_SUCCESS
```

上面代码的处理如下:

(1) 首先检查处理器是否已经进入 VMX operation 模式. 是则直接返回, 否则进行下一步. 处理器 PCB(处理器控制块)中的 Processorstatus 的值记录着当前处理器的状态

(2) 检测是否支持 VMX 架构, 这里并不在其他地方使用独立的函数进行检查.

(3) 开启 `CR4.VMXE` 控制位, 表明允许进入 VMX operation 模式. 详见第 2.2.4 节.

(4) 其中重要的一步是调用 `initialize_vmxon_region` 函数来初始化 VMXON 区域

(5) 接着执行 VMXON 指令, 提供一个 VMXON 区城的物理指针作为操作数, 这个指针被称为 VMXON 指针, 注意这个指针使用的是物理地址

(6) 紧接着对 VMXON 指令进行检查看是否执行成功, 分别检查 CF 与 ZF 标志位, 当 CF=1 或 ZF=1 时, 表明**操作失败**.

VMXON 指今执行成功后, 表明**处理器**此时已经进入 **VMX operation 模式**, 也就是已经处于 **root** 环境, 行使 VMM 的管理职能.