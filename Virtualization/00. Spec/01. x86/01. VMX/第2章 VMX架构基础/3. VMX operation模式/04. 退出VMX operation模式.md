

处理器在 **VMX operation 模式**里**不允许关闭 CR4.VMXE 位(！！！**), 只能在 **VMX operation 模式外**进行关闭, 软件执行 **VMXOFF 指令**将退出 VMX operation 模式.

```x86asm
        ;;
        ;; 检查是否开启 VMX 模式
        ;;
        test DWORD [ebp + PCB.ProcessorStatus], CPU_STATUS_VMXON
        jz vmx_operation_exit.done

        ;;
        ;; 使用 "all-context invalidation" 类型刷新 cache
        ;;
        mov eax, ALL_CONTEXT_INVALIDATION
        invvpid eax, [ebp + PCB.InvDesc]
        invept eax, [ebp + PCB.InvDesc]

        ;;
        ;; 调用 vmxoff 指令
        ;;
        vmxoff

        ;;
        ;; 检查是否成功
        ;; 1) 当 CF = 0 且 ZF = 0 时, VMXOFF 执行成功
        ;;
        mov eax, STATUS_VMXOFF_UNSUCCESS
        jc vmx_operation_exit.done
        jz vmx_operation_exit.done


        ;;
        ;; 下面关闭 CR4.VMXE 标志位
        ;;
        REX.Wrxb
        mov eax, cr4
        btr eax, 13
        REX.Wrxb
        mov cr4, eax

        ;;
        ;; 更新指令状态
        ;;
        and DWORD [ebp + PCB.InstructionStatus], ~INST_STATUS_VMX
        ;;
        ;; 更新处理器状态
        ;;
        and DWORD [ebp + PCB.ProcessorStatus], ~CPU_STATUS_VMXON

        mov eax, STATUS_SUCCESS

vmx_operation_exit.done:
        pop ebp
        ret
```

上面的代码来自 `lib/VMX/VmxInit.asm` 里的 `vmx_operation_exit` 函数, 执行**退出 VMX operation 工作**:

(1) 通过**处理器状态标志**检查当前**是否**处于 `VMX operation` 模式.

(1.5) 还有一个刷新 cache

(2) 执行 **VMXOFF** 指令退出 VMX operation 模式.

(3) 关闭 `CR4.VMXE` 位.

(4) 更新**处理器状态标志位**与**指令状态标志位**.

在执行 VMXOFF 指令后, **必须检查指令是否成功**. 当 `CF=1` 时, 指示**当前的 VMCS 指针无效**, 当 `ZF=1` 时, 指示 **VMXOFF 指令执行遇到错误**. 只有当 CF 与 ZF 标志**同时**为 **0** 时, 才表示 VMXOFF 是成功的！

在 VMX 开启 SMM dual-monitor treatment(SMM 双重监控处理)机制的情况下, 必须先关闭 SMM 双重监控处理机制, 才能使用 VMXOFF 指令关闭 VMX 模式. 否则  VMXOFF 指令将会失败.