>测试无条件与有条件产生 VM\-exit 的 CPUID 和 RDTSC 指令

作为展示 VM\-exit 的例子, 我们只是简单地**打开两个虚拟机运行(分别为 Guest 与 Guest2**)
* Guest1 执行一条**CPUID**指令后, 由 VMM 虚拟化 CPUID 指令.
* Guest2 用来报告**RDTSC**指令引发的 `VM-exit`.

```x86asm
;-----------------------------------------------------------------------
; guest_entry1():
; input:
;       none
; output:
;       none
; 描述:
;       1) 这是 guest1 的入口点
;-----------------------------------------------------------------------
guest_entry1:
        DEBUG_RECORD    "[VM-entry]: switch to guest1 !"        ; 插入 debug 记录点
        DEBUG_RECORD    "[guest]: execute CPUID !"

        ;;
        ;; guest 尝试执行 CPUID.01H
        ;;
        mov eax, 01h
        cpuid

        ;;
        ;; 输出 guest CPU 模型
        ;;
        mov esi, eax
        call get_display_family_model
        mov ebx, eax
        mov esi, GuestCpuMode
        call puts
        mov esi, ebx
        call print_word_value

        hlt
        jmp $ - 1
        ret
```

上面代码片段位于`chap05/ex5-1/ex.asm`文件中, 属于 Guest1 的代码. 为了方便观察执行过程, 在 guest 代码里面插入了两个 debug 记录点(使用`DEBUG_RECORD`宏), 这两个 debug 记录点用来**记录**处理器的**context**及**附加的信息**.

Guest1 尝试查询`CPUID.01H`(`EAX=01H`)信息将会发生`VM-exit`, 由 VMM 接管 CPUID 指令的执行, 下面是**VMM 对 CPUID 指令的虚拟化处理**.

```x86asm
;-----------------------------------------------------------------------
; DoCPUID()
; input:
;       none
; output:
;       none
; 描述:
;       1) 处理尝试执行 CPUID 指令引发的 VM-exit
;-----------------------------------------------------------------------
DoCPUID:
        push ebp
        push ebx
        push ecx
        push edx

%ifdef __X64
        LoadGsBaseToRbp
%else
        mov ebp, [gs: PCB.Base]
%endif

        DEBUG_RECORD    '[DoCPUID]: virtualize CPUID!'

        REX.Wrxb
        mov ebp, [ebp + PCB.CurrentVmbPointer]
        REX.Wrxb
        mov ebp, [ebp + VMB.VsbBase]

        ;;
        ;; 由 VMM 反射一个 CPUID 虚拟化结果给 guest
        ;;
        mov eax, [ebp + VSB.Rax]                                        ; 读取 CPUID 功能号
        cpuid                                                           ; 执行 CPUID 指令
        mov eax, 633h                                                   ; 修改 guest CPU 的型号

        ;;
        ;; 将 CPUID 结果反射给 guest
        ;;
        REX.Wrxb
        mov [ebp + VSB.Rax], eax
        REX.Wrxb
        mov [ebp + VSB.Rbx], ebx
        REX.Wrxb
        mov [ebp + VSB.Rcx], ecx
        REX.Wrxb
        mov [ebp + VSB.Rdx], edx

        ;;
        ;; 调整 guest-RIP
        ;;
        call update_guest_rip

        mov eax, VMM_PROCESS_RESUME                                     ; 通知 VMM 进行 RESUME 操作

        pop edx
        pop ecx
        pop ebx
        pop ebp
        ret
```

这个代码片段位于`lib/VMX/VmxVMM.asm`文件里, DoCPUID 函数将由 VmmEntry 函数调用(同一个文件内), 而**VmmEntry 函数**是**由 VM\-exit 发生后**的**VMM 进入点**(在`TargetCpuVmentry`中设置).

**DoCPUID**函数执行的虚拟化处理如下.

(1) 从**EAX 寄存器**里读取 guest 给 CPUID 指令提供的**功能号**, EAX 寄存器的值在 VmmEntry 函数内使用`STORE_CONTEXT`宏保存在 PCB 的**context 区域**内.

(2) **VMM**执行**CPUID**指令读取**01H 功能信息**. 这里只修改了处理器的型号信息, 其他信息保留不变. EAX 的值修改为 0633H.

(3) VMM 将`CPUID.01H`信息保存在 context 区域内, 用来**给 guest 传递信息**.

(4) 调用 update\_guest\_rip 函数来**更新 guest 的 RIP 值**, 让 RIP 指向 CPUID 的**下一条指令**, 否则将产生死循环(不断发生 VM\-exit 与 VM\-entry).

(5) 最后通过**VmmEntry 函数**进行**RESUME**操作.

```assembly
```

上面代码片段位于 chap05/ex5\-1/ex.asm 文件里, 属于 guest2 的代码. 这个 guest 只是简单执行了一条 RDTSC 指令, 同样也插入了两个 debug 记录点.

```assembly
```

在 Guest2 对应的 TargetCpuVmentry2 函数中, 使用 SET\_PRIMARY\_PROCBASED\_CTLS 宏来设置"RDTSC exiting"位为 1, 是 guest2 执行 RDTSC 指令能产生 VM\-exit.

## 编译与运行

