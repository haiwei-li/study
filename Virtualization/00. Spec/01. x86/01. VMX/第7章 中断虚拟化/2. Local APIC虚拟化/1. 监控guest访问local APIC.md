<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. 不使用 lapic 虚拟化机制下的两种监控模式](#1-不使用-lapic-虚拟化机制下的两种监控模式)
  - [1.1. 监控基于内存映射的 local APIC 访问: EPT violation](#11-监控基于内存映射的-local-apic-访问-ept-violation)
  - [1.2. 监控基于 MSR 的 local APIC 访问: MSR bitmap](#12-监控基于-msr-的-local-apic-访问-msr-bitmap)
- [2. 例子: 使用 EPT 机制实现 local APIC 虚拟化](#2-例子-使用-ept-机制实现-local-apic-虚拟化)
  - [2.1. 监控 guest 访问 IA32_APIC_BASE 寄存器](#21-监控-guest-访问-ia32_apic_base-寄存器)
  - [2.2. 处理写 IA32_APIC_BASE 时产生的 VM-exit](#22-处理写-ia32_apic_base-时产生的-vm-exit)
  - [2.3. 保存 guest 的写入值](#23-保存-guest-的写入值)
  - [2.4. 提供 EPT violation 处理例程](#24-提供-ept-violation-处理例程)
  - [2.5. 处理读 IA32_APIC_BASE 产生的 VM-exit](#25-处理读-ia32_apic_base-产生的-vm-exit)
  - [2.6. 处理 guest 访问 local APIC](#26-处理-guest-访问-local-apic)
    - [2.6.1. 读 APIC-page 页面](#261-读-apic-page-页面)
    - [2.6.2. 写 APIC-page 页面](#262-写-apic-page-页面)
    - [2.6.3. 执行 APIC-page 页面](#263-执行-apic-page-页面)
  - [2.7. guest 端代码](#27-guest-端代码)
  - [2.8. 编译及运行](#28-编译及运行)

<!-- /code_chunk_output -->

# 1. 不使用 lapic 虚拟化机制下的两种监控模式

在**不引入任何 lapic 虚拟化机制**情况下(`7.2.2`节), VMM 通过**EPT 映射机制**或者 **MSR-bitmap** 来限制 guest 访问 local APIC, 其原理是监控 guest 访问 local APIC 的行为并做出相应处理.

## 1.1. 监控基于内存映射的 local APIC 访问: EPT violation

当 local APIC 使用 **xAPIC** 模式, **local APIC 寄存器**通过 `memory-mapped`(内存映射)方式映射在**物理空间**的**4K 页面**上, 我们可以称这个 4K 页面为`APIC-page`.

`APIC-page`**物理基址**在`IA32_APIC_BASE`寄存器的`bits N-1:12`里提供(N=MAXPHYADDR)

注: local APIC 的寄存器一共占据 4K 空间, 这个 `APIC-page` 就是正常情况下的描述, 不是虚拟化情况下的.

在这种情况下, VMM 可以通过**EPT 映射机制！！！** (EPT violation)来监控 guest 访问 local APIC.

## 1.2. 监控基于 MSR 的 local APIC 访问: MSR bitmap

当 local APIC 使用 **x2APIC** 模式(`xAPIC` 的扩展模式), local APIC 寄存器映射到 **MSR 空间！！！** 上, MSR 地址范围为`800H ~ 8FFH`. guest 软件使用`RDMSR`指令读取 local APIC 寄存器. 使用 WRMSR 指令写入 local APIC 寄存器.

在这种情况下, VMM 可以通过设置 **MSR-bitmap！！！** 来监控 guest 访问 local APIC.

# 2. 例子: 使用 EPT 机制实现 local APIC 虚拟化

>示例 7-3: 使用 EPT 机制实现 local APIC 虚拟化

上文提及, 当 local APIC 使用**xAPIC**模式时, 我们可以通过**EPT 映射机制**来实现监控 guest 访问 localAPIC 在这一节里,  我们将使用 EPT 映射机制来实现 local APIC 虚拟化.

## 2.1. 监控 guest 访问 IA32_APIC_BASE 寄存器

VMM 必须监控 guest 对 `IA32_APIC_BASE` 寄存器的访问(包括**读与写访问**), 只有这样才能够监控 guest 访问 local APIC 寄存器, 如代码片段 7-16 所示.

```x86asm
;;
;; 设置拦截对 IA32_APIC_BASE 的读操作
;;
mov esi, IA32_APIC_BASE
call set_msr_read_bitmap

;;
;; 设置拦截对 IA32_APIC_BASE 的写操作
;;
mov esi, IA32_APIC_BASE
call set_msr_write_bitmap
```

在 `chap07\ex7-3\ex.asm` 模块的 `init_guest_a` 与`init_guest_b`函数里, 分别设置了对`IA32_APIC_BASE`的**读/写**进行拦截.
* `set_msr_read_bitmap` 函数**设置 MSR 的读访问限制**
* `set_msr_write__bitmap` 函数设置**MSR 的写访问限制**

## 2.2. 处理写 IA32_APIC_BASE 时产生的 VM-exit

当 guest 执行**WRMSR 指令**尝试写`IA32_APIC_BASE`寄存器时产生`VM-exit`.

例如, 下面的 `chap07\ex7-3\guest_ex.asm` 模块代码片段 7-17 所示.

```x86asm
;;
;; 设置 local APIC base 值为 01000000h
;;
mov ecx, IA32_APIC_BASE
mov eax, 01000000h | APIC_BASE_BSP | APIC_BASE_ENABLE
xor edx, edx
wrmsr
```

guest 试图设置 `APIC-page` 的**基址**为 `01000000H` . **ECX 寄存器**表示操作的是哪个 MSR 寄存器.

在产生 `VM-exit` 后, VMM 调用**DoWRMSR 函数**处理由于**WRMSR 指令**而产生的 `VM-exit` .

注: 具体处理从`lib/VMX/VmxVMM.asm`中的`VmmEntry`开始, 根据`exit code`执行对应的处理例程.

```x86asm
DoWRMSR:

        ......

        ;;
        ;; 读取 MSR index
        ;;
        mov ecx, [ebx + VSB.Rcx]
        cmp ecx, IA32_APIC_BASE
        jne DoWRMSR.@1

        ;;
        ;; 处理写 IA32_APIC_BASE 寄存器
        ;;
        call DoWriteMsrForApicBase

```

**DoWRMSR 函数**实现在`lib\Vmx\VmxVMM.asm`文件里. 它读取`VM-exit`时的**ECX 寄存器値**, 属于`IA32_APIC_BASE`寄存器吋, 调用 `DoWriteMsrForApicBase` 来处理 guest 对`IA32_APIC_BASE`寄存器的**写操作**.

```x86asm
;-----------------------------------------------------------------------
; DoWriteMsrForApicBase()
; input:
;       none
; output:
;       none
; 描述:
;       1) 处理 guest 访问 IA32_APIC_BASE 寄存器
;-----------------------------------------------------------------------
DoWriteMsrForApicBase:
        push ebp
        push ebx
        push edx
        push ecx

%ifdef __X64
        LoadGsBaseToRbp
%else
        mov ebp, [gs: PCB.Base]
%endif
        REX.Wrxb
        mov ebx, [ebp + PCB.CurrentVmbPointer]
        REX.Wrxb
        mov ebx, [ebx + VMB.VsbBase]

        ;;
        ;; 读取 guest 写入的 MSR 值
        ;;
        mov eax, [ebx + VSB.Rax]
        mov edx, [ebx + VSB.Rdx]

        DEBUG_RECORD    "[DoWriteMsrForApicBase]: write to IA32_APIC_BASE"

        ;;
        ;; ### 检查写入值是否合法 ###
        ;; 1) 保留位(bits 7:0, bit 9, bits 63:N)需为 0
        ;; 2) 检查 bit 11 与 bit 10 的设置
        ;;      a) 当 bit 11 = 1, bit 10 = 0 时, 设置 bit 11 = 1,  bit 10 = 1 开启 x2APIC 模式
        ;;      b) 当 bit 11 = 0, bit 10 = 1 时, 无效
        ;;      c) 当 bit 11 = 0, bit 10 = 0 时, 关闭 local APIC
        ;;      d) 当 bit 11 = 1, bit 10 = 1 时, 设置 bit 11 = 1, bit 10 = 0 时, 产生 #GP 异常
        ;;

        ;;
        ;; 检查保留位, 不为 0 时注入 #GP 异常
        ;;
        test eax, 2FFh
        jnz DoWriteMsrForApicBase.Error
        mov esi, [ebp + PCB.MaxPhyAddrSelectMask + 4]
        not esi
        test edx, esi
        jnz DoWriteMsrForApicBase.Error

        ;;
        ;; 检查 xAPIC enable(bit 11)与 x2APIC enable(bit 10)
        ;;
        test eax, APIC_BASE_X2APIC
        jz DoWriteMsrForApicBase.Check.@1

        ;;
        ;; 当 bit 10 = 1 时, 检查 CPUID.01H:ECX[21].x2APIC 位
        ;; 1) 为 0 时表明不支持 x2APIC 模式, 注入 #GP(0) 异常
        ;;
        test DWORD [ebp + PCB.CpuidLeaf01Ecx], (1 << 21)
        jz DoWriteMsrForApicBase.Error

        ;;
        ;; 当 bit 10 = 1 时, bit 11 = 0, 无效设置则注入 #GP(0) 异常
        ;;
        test eax, APIC_BASE_ENABLE
        jz DoWriteMsrForApicBase.Error


DoWriteMsrForApicBase.x2APIC:
        ;;
        ;; 现在 bit 10 = 1, bit 11 = 1
        ;; 1) 使用 x2APIC 模式的虚拟化设置
        ;;
        mov esi, IA32_APIC_BASE
        call AppendMsrVte                                ;; 保存 guest 写入原值


        ;;
        ;; 检查 secondary prcessor-based VM-execution control 字段"virtualize x2APIC mode"位
        ;; 1) 为 1 时, 使用 VMX 原生的 x2APIC 虚拟化, 直接返回
        ;; 2) 为 0 时, 监控 800H - 8FFH MSR 的读写
        ;;
        GetVmcsField    CONTROL_PROCBASED_SECONDARY
        test eax, VIRTUALIZE_X2APIC_MODE
        jnz DoWriteMsrForApicBase.Done

        ;;
        ;; 现在监控 x2APIC MSR 的读写, 范围从 800H 到 8FFH
        ;;
        call set_msr_read_bitmap_for_x2apic
        call set_msr_write_bitmap_for_x2apic
        jmp DoWriteMsrForApicBase.Done

DoWriteMsrForApicBase.Check.@1:
        ;;
        ;; bit 10 = 0, bit 11 = 0, 关闭 local APIC, 不进行虚拟化处理
        ;; 1)写入 IA32_APIC_BASE 寄存器
        ;; 2)恢复映射
        ;;
        test eax, APIC_BASE_ENABLE
        jnz DoWriteMsrForApicBase.Check.@2

        ;;
        ;; guest 尝试关闭 local APIC
        ;; 1) 恢复 guest 对 IA32_APIC_BASE 寄存器的写入
        ;; 2) 恢复 EPT 映射
        ;;
        mov esi, IA32_APIC_BASE
        mov eax, [ebx + VSB.Rax]
        mov edx, [ebx + VSB.Rdx]
        call append_vmentry_msr_load_entry

%ifdef __X64
        REX.Wrxb
        mov esi, [ebx + VSB.Rax]
        mov edi, 0FEE00000h
        mov eax, EPT_WRITE | EPT_READ
        call do_guest_physical_address_mapping
%else
        mov esi, [ebx + VSB.Rax]
        mov edi, [ebx + VSB.Rdx]
        mov eax, 0FEE00000h
        mov edx, 0
        mov ecx, EPT_WRITE | EPT_READ
        call do_guest_physical_address_mapping
%endif

        jmp DoWriteMsrForApicBase.Done

DoWriteMsrForApicBase.Check.@2:
        ;;
        ;; 读取原 guest 设置的 APIC_APIC_BASE 值
        ;; 1) 假如返回 0 值, 则表明 guest 第 1 次写 IA32_APIC_BASE
        ;;
        mov esi, IA32_APIC_BASE
        call GetMsrVte
        test eax, eax
        jz DoWriteMsrForApicBase.xAPIC

        ;;
        ;; 如果原值 bit 11 = 1, bit 10 = 1 时, 当设置 bit 11 = 1, bit 10 = 0 时, 将产生 #GP 异常
        ;;
        test DWORD [eax + MSR_VTE.Value], APIC_BASE_X2APIC
        jnz DoWriteMsrForApicBase.Error


DoWriteMsrForApicBase.xAPIC:
        ;;
        ;; ### 下面虚拟化 local APIC 的 xAPIC 模式 ###
        ;;
        mov esi, IA32_APIC_BASE
        mov eax, [ebx + VSB.Rax]
        mov edx, [ebx + VSB.Rdx]
        call AppendMsrVte                               ; 保存 guest 写入值

        REX.Wrxb
        mov edx, eax

        ;;
        ;; 1)检查是否开启了"virtualize APIC access "
        ;;     a) 是, 则设置 APIC-access page 页面
        ;;     b) 否, 则提供 GPA 例程处理 local APIC 访问
        ;; 2)检查是否开启了"enable EPT"
        ;;     a)是, 则映射 IA32_APIC_BASE[N-1:12], 将 APIC-access page 设置为该 HPA 值
        ;;     b)否, 则直接将 IA32_APIC_BASE[N-1:12] 设为 APIC-access page
        ;;

        GetVmcsField    CONTROL_PROCBASED_SECONDARY

        test eax, VIRTUALIZE_APIC_ACCESS
        jz DoWriteMsrForApicBase.SetForEptViolation
        test eax, ENABLE_EPT
        jz DoWriteMsrForApicBase.EptDisable

        ;;
        ;; 执行 EPT 映射到 0FEE00000H
        ;;
%ifdef __X64
        REX.Wrxb
        mov esi, [edx + MSR_VTE.Value]
        mov edi, 0FEE00000h
        mov eax, EPT_READ | EPT_WRITE
        call do_guest_physical_address_mapping
%else
        mov esi, [edx + MSR_VTE.Value]
        mov edi, [edx + MSR_VTE.Value + 4]
        mov eax, 0FEE00000H
        mov edx, 0
        mov ecx, EPT_READ | EPT_WRITE
        call do_guest_physical_address_mapping
%endif

        mov eax, 0FEE00000h
        mov edx, 0
        jmp DoWriteMsrForApicBase.SetApicAccessPage


DoWriteMsrForApicBase.EptDisable:
        REX.Wrxb
        mov eax, [edx + MSR_VTE.Value]
        mov edx, [edx + MSR_VTE.Value + 4]
        REX.Wrxb
        and eax, ~0FFFh

DoWriteMsrForApicBase.SetApicAccessPage:
        SetVmcsField    CONTROL_APIC_ACCESS_ADDRESS_FULL, eax
%ifndef __X64
        SetVmcsField    CONTROL_APIC_ACCESS_ADDRESS_HIGH, edx
%endif

        call update_guest_rip
        jmp DoWriteMsrForApicBase.Done


DoWriteMsrForApicBase.SetForEptViolation:
        ;;
        ;; 处理 guest 写入 IA32_APIC_BASE 寄存器的值:
        ;; 1)将 IA32_APIC_BASE[N-1:12] 映射到 host 的 IA32_APIC_BASE 值, 但是为 not-present
        ;; 2)GPA 不进行任何映射
        ;;

        ;;
        ;; 为 GPA 提供处理例程
        ;;
        REX.Wrxb
        mov esi, [edx + MSR_VTE.Value]
        REX.Wrxb
        and esi, ~0FFFh
        mov edi, EptHandlerForGuestApicPage
        call AppendGpaHte


        call update_guest_rip
        jmp DoWriteMsrForApicBase.Done

DoWriteMsrForApicBase.Error:
        ;;
        ;; 反射 #GP(0) 给 guest 处理
        ;;
        SetVmcsField    VMENTRY_INTERRUPTION_INFORMATION, INJECT_EXCEPTION_GP
        SetVmcsField    VMENTRY_EXCEPTION_ERROR_CODE, 0

DoWriteMsrForApicBase.Done:
        pop ecx
        pop edx
        pop ebx
        pop ebp
        ret
```

`DoWriteMsrForApicBase` 函数实现在 `lib\Vmx\Vmxmsr.asm` 文件里, 它根据 guest 设置 **local APIC** 的**模式**(**XAPIC** 还是 **x2APIC**), 以及 `secondary processor-based VM-execution control` 字段的设置进行虚拟化配置.

* 当 local APIC 为 **XAPIC** 模式时
    * 如果 "**virtualize APIC accesses**" 为 1, 则使用 VMX 提供的 **local APIC 虚拟化功能**.
    * 如果 "**virtualize APIC accesses**" 为 0, 则利用 **EPT violation** 故障来达到 local APIC 虚拟化.
* 当 local APIC 为 **x2APIC** 模式时
    * 如果 "**virtualize x2 APIC mode**" 为 1, 则使用 VMX 提供的 **x2APIC 模式虚拟化**功能.
    * 如果 "**virtualize x2 APIC mode**" 为 0, 则利用 **MSR bitmap** 来达到 local APIC 虚拟化.

`DoWriteMsrForApicBase` 例程通过检査 guest 写入 **IA32 APIC BASE** 寄存器的**值**确定 guest 的 local APIC 处于**何种工作模式**. 在检查**写入值不合法**时将**注入** `#GP` 异常给 guest 处理, 例如保留位(bits 63:N、bit 9 以及 bits 7:0) 必须为 0.

当利用 **EPT violation** 来虚拟化 local APIC 时, 下面是简单的处理流程.

(1) 读取 EAX 与 EDX 寄存器值. 两个寄存器组合的 64 位值是 guest 尝试写入 `IA32_APIC_BASE` **寄存器的值**.

(2) **保存** guest 写入 `IA32_APIC_BASE` 寄存器的**值**. 这个值在 guest 读取 `IA32_APIC_BASE` 寄存器时需要**反馈给 guest**

(3) 为 guest 的写入值提供一个 `EPT violation` 处理例程.

## 2.3. 保存 guest 的写入值

在前面介绍的 `DoWriteMsrForApicBase` 函数里, 使用 `AppendMsrVte` 函数将 guest 的**写入值**保存在了 guest 对应的 **MSR VTE** (Value Table Entry) 区域里.

这个 MSR VTE 区域在 `initialize_vmcs_buffer` 函数初始化时从 **kernel pool** 里分配. **这个不属于 VMCS 区域**, 可以看`3.11`的实例代码.

在 `inc\vmcs.inc` 文件里定义了一个 `MSR_VTE` 结构, 这个结构用来定义 MSR VTE 区域里的每个表项.

```inc
;;
;; MSR 值列表项结构
;;
struc MSR_VTE
        .MsrIndex                       RESD    1
        .Value                          RESQ    1

        MSR_VTE_SIZE                    EQU     $
endstruc
```

`AppendMsrVte` 函数的定义实现在 `lib\Vmx\VmxMsr.asm` 文件

## 2.4. 提供 EPT violation 处理例程

由于 VMM **拦截**guest 写 `IA32_APIC_BASE` 寄存器, 但并**没有**为 guest 写入的 guest-physical address 地址值进行 **EPT 映射**. 因此, 当 **guest** 尝试**访问** local APIC **寄存器**时, 会产生 EPT violation 故障.

那么, 在 `DoWriteMsrForApicBase` 函数里使用 `AppendGpaHte` 函数提供了由这个 `guest-physical address` 产生 EPT violation 而导致 VM-exit 的处理例程.

```x86asm

```

`AppendGpaHte` 函数的定义看起来像上面的 C 代码, 实现在 `lib\Vmx\VmxPage.asm` 文件里. 在 **GPA HTE** (Handler Table Entry)区域建立 **GPA** 与 EPT violation 处理例程的**对应关系**. 这个 GPA HTE 区域在 `initialize_vmcs_buffer` 函数初始化时在 kernel pool 里分配. GPA HTE 结构定义在 `inc\vmcs.inc` 文件里.

## 2.5. 处理读 IA32_APIC_BASE 产生的 VM-exit

当 guest 尝试**读取** `IA32_APIC_BASE` 寄存器的值时, VMM 拦截了 RDMSR 指令而产生 `VM-exit`.

DORDMSR 例程检查到 **ECX** 的值是 `IA32_APIC_BASE` 寄存器时, 调用 `DoReadMsrForApicBase` 例程来处理.

```x86asm
;-----------------------------------------------------------------------
; DoReadMsrForApicBase()
; input:
;       none
; output:
;       none
; 描述:
;       1) 处理 guest 读 IA32_APIC_BASE 寄存器
;-----------------------------------------------------------------------
DoReadMsrForApicBase:
        push ebp
        push ebx
        push edx

%ifdef __X64
        LoadGsBaseToRbp
%else
        mov ebp, [gs: PCB.Base]
%endif
        REX.Wrxb
        mov ebx, [ebp + PCB.CurrentVmbPointer]
        REX.Wrxb
        mov ebx, [ebx + VMB.VsbBase]

        mov esi, IA32_APIC_BASE
        call GetMsrVte
        REX.Wrxb
        test eax, eax
        jz DoReadMsrForApicBase.Done

        mov edx, [eax + MSR_VTE.Value + 4]
        mov eax, [eax + MSR_VTE.Value]
        mov [ebx + VSB.Rax], eax
        mov [ebx + VSB.Rdx], edx

        DEBUG_RECORD    "[DoWriteMsrForApicBase]: read from IA32_APIC_BASE"

        call update_guest_rip
DoReadMsrForApicBase.Done:
        pop edx
        pop ebx
        pop ebp
        ret
```

`DoReadMsrForApicBase` 函数实现在`lib\Vmx\VmxMsr.asm`文件里, 它的主要工作是:

(1) 调用 GetMsrVte 函数从**MSR VTE 区域**中找到**对应的 VTE 表项**.

(2) 从 VTE 表项里读取 guest 尝试写入 `IA32_APIC_BASE` 寄存器里的**原值**, 写入 **guest 对应的 VSB 区域**中.

```
        ;; 一个 VMB 结构体对应一个虚拟机实例
        ;; 获取 VMB 中的 VSB Base
        mov ebx, [ebx + VMB.VsbBase]
        ;; 从 VTE 表项读取原值
        mov edx, [eax + MSR_VTE.Value + 4]
        mov eax, [eax + MSR_VTE.Value]
        ;; 将原值写入 guest 中对应的 VSB 区域
        ;; ebx 是该 VM 对应的 VSB Base
        ;; 原值写入 VSB 的 RAX 域和 RDX 域
        mov [ebx + VSB.Rax], eax
        mov [ebx + VSB.Rdx], edx
```

关于 VSB 结构, 详见`3.11.1. VMCS 相关数据结构.md`

GetMsrVte 函数实现在 `lib\Vmx\VmxMsr.asm` 文件里,

当没有 MSR 对应的 MSR VTE 表项时, 返回 0 值. 从 VTE 表项**读取 MSR 值**复制到 VSB(VM Store Block) 区域后, 在执行 VMRESUME 指令前, 从 **VSB 区域**恢复 guest 的**context 信息**.

## 2.6. 处理 guest 访问 local APIC

guest 尝试**读或者写 APIC-page** 的值时, 由于 APIC-page 的基址(`IA32_APIC_BASE`提供)并**没有 EPT 映射**, 从而导致由于 EPT violation 故障而产生 VM-exit.

```
        ;;
        ;; 读取发生 EPT violation 的 guest-physical address 值
        ;;
        REX.Wrxb
        mov ebx, [ebp + PCB.ExitInfoBuf + EXIT_INFO.GuestPhysicalAddress]

        ;;
        ;; 检查 GPA 是否需要进行额外处理
        ;;
        REX.Wrxb
        mov esi, ebx
        REX.Wrxb
        and esi, ~0FFFh
        call GetGpaHte
        REX.Wrxb
        test eax, eax
        jz DoEptViolaton.next
        REX.Wrxb
        mov eax, [eax + GPA_HTE.Handler]
        call eax

        ;;
        ;; 处理完毕后, 是否需要修复 EPT violation 故障
        ;; a)需要则执行下面的修复工作
        ;; b)否则直接返回
        ;;
        cmp eax, EPT_VIOLATION_FIXING
        jne DoEptViolation.resume

DoEptViolaton.next:
```

上面代码来自`DoEptViolaton`例程, 它**读取**引发 EPT violation 故障的**GPA 地址值**, 然后检查这个 GPA 是否需要额外处理.

使用 `GetGpaHte` 函数来查找**GPA 地址值**对应的**HTE**(Handler Table Entry)表项, 如果找到则调用 GPA 对应的处理例程来处理. 处理完毕后检查返回值来确定是否需要修复 EPT violation 故障.

```
;-----------------------------------------------------------------------
; EptHandlerForGuestApicPage()
; input:
;       none
; output:
;       eax - 处理码
; 描述:
;       1) 处理由于 guest APIC-page 而引起的 EPT violation
;-----------------------------------------------------------------------
EptHandlerForGuestApicPage:
        push ebp
        push ebx
        push ecx
        push edx

%ifdef __X64
        LoadGsBaseToRbp
%else
        mov ebp, [gs: PCB.Base]
%endif


        REX.Wrxb
        mov ebx, [ebp + PCB.CurrentVmbPointer]

        ;;
        ;; EPT violation 明细信息
        ;;
        mov eax, [ebp + PCB.ExitInfoBuf + EXIT_INFO.ExitQualification]

        ;;
        ;; guest 访问 APIC-page 的偏移量
        ;;
        REX.Wrxb
        mov edx, [ebp + PCB.ExitInfoBuf + EXIT_INFO.GuestPhysicalAddress]
        and edx, 0FFFh

        ;;
        ;; 检查 guest 访问类型
        ;;
        test eax, EPT_READ
        jnz EptHandlerForGuestApicPage.Read
        test eax, EPT_EXECUTE
        jz EptHandlerForGuestApicPage.Write

        ;;
        ;; 处理 guest 尝试执行 APIC-page 页面, 注入一个 #PF(0x11) 异常
        ;;
        SetVmcsField    VMENTRY_INTERRUPTION_INFORMATION, INJECT_EXCEPTION_PF
        SetVmcsField    VMENTRY_EXCEPTION_ERROR_CODE, 0011h
        REX.Wrxb
        mov eax, [ebp + PCB.ExitInfoBuf + EXIT_INFO.GuestLinearAddress]
        REX.Wrxb
        mov cr2, eax
        jmp EptHandlerForGuestApicPage.Done

EptHandlerForGuestApicPage.Write:
        ;;
        ;; 读取源操作数值
        ;;
        GetVmcsField    GUEST_RIP
        REX.Wrxb
        mov esi, eax
        call get_system_va_of_guest_va
        REX.Wrxb
        mov esi, eax
        mov al, [esi]                           ; opcode
        cmp al, 89h
        je EptHandlerForGuestApicPage.Write.Opcode89
        cmp al, 0C7h
        je EptHandlerForGuestApicPage.Write.OpcodeC7

        ;;
        ;; ### 注意, 作为示例, 这里不处理其它指令情况, 包括:
        ;; 1) 使用其它 opcode 的指令
        ;; 1) 含有 REX prefix(4xH) 指令
        ;;
        jmp EptHandlerForGuestApicPage.Done

EptHandlerForGuestApicPage.Write.OpcodeC7:
        ;;
        ;; 分析 ModRM 字节
        ;;
        mov al, [esi + 1]
        mov cl, al
        and ecx, 7
        cmp cl, 4
        sete cl                                 ; 如果 ModRM.r/m = 4, 则 cl = 1, 否则 cl = 0
        shr al, 6
        jz EptHandlerForGuestApicPage.Write.@2  ; ModRM.Mod = 0, 则 ecx += 0
        cmp al, 1                               ; ModRM.Mod = 1, 则 ecx += 2
        je EptHandlerForGuestApicPage.Write.OpcodeC7.@1
        add ecx, 2
EptHandlerForGuestApicPage.Write.OpcodeC7.@1:
        add ecx, 2                              ; ModRM.Mod = 2, 则 ecx += 4
                                                ; ModRM.Mod = 3, 属于错误 encode
EptHandlerForGuestApicPage.Write.@2:
        ;;
        ;; 读取写入立即数
        ;;
        mov eax, [esi + ecx + 2]

        jmp EptHandlerForGuestApicPage.Write.Next

EptHandlerForGuestApicPage.Write.Opcode89:
        ;;
        ;; 读取源操作数
        ;;
        mov esi, [esi + 1]
        shr esi, 3
        and esi, 7
        call get_guest_register_value

EptHandlerForGuestApicPage.Write.Next:
        ;;
        ;; virtual APIC-page 页面
        ;;
        REX.Wrxb
        mov esi, [ebx + VMB.VirtualApicAddress]

        ;;
        ;; APIC-page 可写的 offset 为, 写其它区域忽略
        ;; 1) 80h:      TPR
        ;; 2) B0h:      EOI
        ;; 3) D0h:      LDR
        ;; 4) E0h:      DFR
        ;; 5) F0h:      SVR
        ;; 6) 2F0h - 370h:      LVT
        ;; 7) 380h:     TIMER-ICR
        ;; 8) 3E0h:     TIMER-DCR
        ;;
        cmp edx, 80h
        jne EptHandlerForGuestApicPage.Write.@1

        DEBUG_RECORD    "[EptHandlerForGuestApicPage]: wirte to APIC-page"

        ;;
        ;; 写入 TPR
        ;;
        mov [esi + 80h], eax
        jmp EptHandlerForGuestApicPage.Done

EptHandlerForGuestApicPage.Write.@1:

        jmp EptHandlerForGuestApicPage.Done



EptHandlerForGuestApicPage.Read:
        ;;
        ;; 分析指令
        ;;
        GetVmcsField    GUEST_RIP
        REX.Wrxb
        mov esi, eax
        call get_system_va_of_guest_va
        REX.Wrxb
        mov esi, eax
        mov al, [esi]                           ; opcode
        cmp al, 8Bh
        je EptHandlerForGuestApicPage.Read.Opcode8B

        ;;
        ;; ### 注意, 作为示例, 这里不处理其它指令情况, 包括:
        ;; 1) 使用其它 opcode 的指令
        ;; 1) 含有 REX prefix(4xH) 指令
        ;;
        jmp EptHandlerForGuestApicPage.Done

EptHandlerForGuestApicPage.Read.Opcode8B:
        ;;
        ;; 读取目标操作数 ID
        ;;
        mov esi, [esi + 1]
        shr esi, 3
        and esi, 7

        ;;
        ;; APIC-page 内下面的 offset 为可读区域
        ;; 1) 20h:      APIC ID
        ;; 2) 30h:      VER
        ;; 3) 80h:      TPR
        ;; 4) 90h:      APR
        ;; 5) A0h:      PPR
        ;; 6) B0h:      EOI
        ;; 7) C0h:      RRD
        ;; 8) D0h:      LDR
        ;; 9) E0h:      DFR
        ;; 10) F0h:     SVR
        ;; 11) 100h - 170h:     ISR
        ;; 12) 180h - 1F0h:     TMR
        ;; 13) 200h - 270h:     IRR
        ;; 14) 280h:    ESR
        ;; 15) 2F0h - 370h:     LVT
        ;; 16) 380h:    TIMER-ICR
        ;; 17) 3E0h:    TIMER-DCR
        ;;

        cmp edx, 80h
        jne EptHandlerForGuestApicPage.Read.@1

        DEBUG_RECORD    "[EptHandlerForGuestApicPage]: read from APIC-page"

        ;;
        ;; 写入目标寄存器
        ;;

        REX.Wrxb
        mov eax, [ebx + VMB.VirtualApicAddress]
        mov edi, [eax + 80h]
        call set_guest_register_value
        jmp EptHandlerForGuestApicPage.Done

EptHandlerForGuestApicPage.Read.@1:

EptHandlerForGuestApicPage.Done:
        call update_guest_rip
        mov eax, EPT_VIOLATION_NO_FIXING
        pop edx
        pop ecx
        pop ebx
        pop ebp
        ret
```

EptHandlerForGuestApicPage 例程用来模拟 guest 访间 local APIC, 这是一个相当烦琐的操作. 这个函数实现在 `lib\Vmx\VmxApic.asm` 文件里. 作为示例, 这个函数只实现了基本的**框架功能**, 并**没有处理所有可能发生的情况**.

这个例程需要处理 guest 的两大类操作: **读和写 APIC-page 区城**. VMCS 区城的 **Exit Qualification** 字段记录 EPT violation 的明细信息, 其中 `bis 2:0` 指示访问的类型(参见 3.10.1.14 节).

* bit 0 为 1 时, 表示进行了读访问.
* bit 1 为 1 时, 表示进行了写访问.
* bit 2 为 1 时, 表示进行了执行访问.

EptHandlerForGuestApicPage 例程可以根据 Exit Qualification 字段的 `bits 2:0` 来判断 guest 当前对 APIC-page 进行了什么访问, 从而做出相应的处理.

### 2.6.1. 读 APIC-page 页面

当属于**读 APIC-page 页面**时, 这个例程进行下面的处理

 (1) **分析 guest 访问指令**. 典型地, 一个读访问使用 MOV 指令来完成, 这个例程**只处理了 MOV 指令**(**opcode** 码为 **8BH**). 另外, local APIC 不能支持超过 32 位的访问宽度, 并且某些处理器也不支持低于 32 位的访问宽度(譬如 16 位). 这里也没有处理 guest 使用 64 位的访问宽度的情况(即指令存在 REX prefix

 (2) 读取**目标操作数 ID 值**. 使用 MOV 指令读 APIC-page 时, 目标操作数必定是**寄存器**. 通过分析**指令编码**的 MODRM 字段来获取目标操作数的 ID 值.

 (3) 分析读 APIC-page 页面的 **offset** 值是否合法. APIC-page 的某些区域是保留的访问这些保留区域是未定义的. 因此, EptHandlerForGuestApicPage 例程需要检查访问的 offset 是否合法. 作为示例, 这里只处理了访问 TPR 寄存器的情况(offset 为 80H).

 (4) 从 virtual- APIC page 页面的 80H (TPR)读取相应的值写入目标寄存器里.

这个 **virtual- APIC page** 是 **APIC-page** 的**虚拟页面**, 每个 guest 都有自己**独立的虚拟的 APIC-page 页面**, 用来存放 local APIC 的虚拟数据. 当**写 local APIC 时**往这个虚拟的页面**写入数据**, 当读 local APIC 时从这个虚拟的页面读取数据.

### 2.6.2. 写 APIC-page 页面

当属于写 APIC-page 页面时, 这个例程进行下面的处理:

(1) 分析 guest 指令. 写 local APIC 操作的指令相对更丰富些. 典型地, 可以有下面的指令形式:

```
mov  [ebx + TPR], 50h
mov  eax. 50h
mov  [ebx + TPR], eax
```

这两条写 local APIC 的 MOV 指令, 它们的 **opcode** 码是**不同的**. 作为示例, 在这个例程只处理了 opcode 码为 89H 以及 CH 时的指令情况.

* 指令 opcode 码为 89H 时, 源操作数是寄存器. 从 MODRM 字节的 bits5:3 读取寄存器 ID 值, 然后使用 get guest register value 函数读取寄存器值.
* 指令 opcode 码为 C7H 时, 源操作数是 32 位立即数, 则分析 MODRM 字节是否存在 SIB 以及 displacement 值, 然后从指令 encode 中读取 32 位的立即数值.

(2) 分析写人 APIC-page 的 offset 值, 检査是否属于 local APIC 可写的寄存器(只读寄存器以及保留区城都不可写). 作为示例, 这里只处理了 TPR 寄存器(80H).

### 2.6.3. 执行 APIC-page 页面

当 guest 尝试执行 APIC-page 页面时, 这个例程反射一个 `#F` 异常给 guest 处理, 错误码为 0011H, 指示页面不可执行. 将 CR2 寄存器设置为 guest-linear address 值. 注意: 这个处理实际上与 guest 相关, 要取决于 guest 如何映射 APIC-page 区域.

一个实用的 Epthandlerforguestapicpage 例程需要完善很多细节, 各种可能遇到的情况都要考虑进去, 需要进行烦琐的处理工作才能完全模拟 guest 访问 local APIC.

## 2.7. guest 端代码

在`chap07\ex7-3`目录下`guest_ex.asm`文件是这个例子的 guest 主体代码.

```
        ;;
        ;; 加入 ex.asm 模块使用的头文件
        ;;
        %include "ex.inc"


        ;;
        ;; 例子 ex7-3: 实现 local APIC 虚拟化
        ;; 编译命令可以为:
        ;;      1) build -DDEBUG_RECORD_ENABLE -DGUEST_ENABLE -D__X64 -DGUEST_X64
        ;;      2) build -DDEBUG_RECORD_ENABLE -DGUEST_ENABLE -D__X64
        ;;      3) build -DDEBUG_RECORD_ENABLE -DGUEST_ENABLE
        ;;


        ;;
        ;; 设置 local APIC base 值为 01000000h
        ;;
        mov ecx, IA32_APIC_BASE
        mov eax, 01000000h | APIC_BASE_BSP | APIC_BASE_ENABLE
        xor edx, edx
        wrmsr

        mov esi, GuestEx.Msg0
        call PutStr
        mov ecx, IA32_APIC_BASE
        rdmsr
        mov esi, eax
        and esi, ~0FFFh
        mov edi, edx
        call PrintQword
        call PrintLn

        mov R3, GUEST_APIC_BASE

        ;;
        ;; TPR = 50h
        ;;
        mov eax, 50h
        mov [R3 + LAPIC_TPR], eax
        mov esi, GuestEx.Msg1
        call PutStr
        mov esi, [R3 + LAPIC_TPR]
        call PrintValue
        call PrintLn

        ;;
        ;; TPR = 60h
        ;;
        mov DWORD [R3 + LAPIC_TPR], 60h
        mov esi, GuestEx.Msg1
        call PutStr
        mov esi, [R3 + LAPIC_TPR]
        call PrintValue

        jmp $

GuestEx.Msg0    db      'APIC base: ', 0
GuestEx.Msg1    db      'TPR:       ', 0
```

这个 guest 代码的工作是:

 (1) 尝试将 IA32 APIC BASE 寄存器的 bisN-1:12 设置为 0100000O(即新的

le Markup Toolbar

APIC-page 基址).

 (2) 从 1A32 APIC BASE 寄存器读取 APIC-page 基址, 并打印出来

 (3) 尝试向 local APIC 的 TPR 寄存器(80H)写人值 50H. 注意, 这里 MOV 指令使

用寄存器作为源操作数(即 opcode 码为 89H).

 (4) 读取 TPR 寄存器(80H), 并打印出来

 (5) 尝试再次向 TPR 寄存器(80H)写入值 60H. 注意, 这里 MOV 指令使用立即数作为源操作数(即 opcode 码为 C7H).

 (6) 再次读取 TPR 寄存器(80H), 并打印出来.

## 2.8. 编译及运行

这个例子, 我们可以使用下面的命令进行编译

1) `build -DDEBUG_RECORD_ENABLE -DGUEST_ENABLE -D__X64 -DGUEST_X64`

2) `build -DDEBUG_RECORD_ENABLE -DGUEST_ENABLE -D__X64`

3) `build -DDEBUG_RECORD_ENABLE -DGUEST_ENABLE`

第 1 个命令将 host 与 guest 都编译为 64 位. 第 2 个命令将 host 编译为 64 位, guest 编译为 32 位. 第 3 个命令将 host 与 guest 都编译为 32 位.

下面以 host 与 guest 都是 64 位环境为例, 观察例子的运行结果.

在 Vmware 里加载 demo. Img 映像文件运行, 在 CPU0 的命令选择器上按数字《1> 键

让 CPUI 进入 guest 运行. 按下两次《F2> 键切换到 CPUI 的 guest 屏幕, 如图 7-5 所示.