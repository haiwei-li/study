
<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [1. 任务切换以及 VMM 的模拟](#1-任务切换以及-vmm-的模拟)
- [2. 检查任务切换条件](#2-检查任务切换条件)
  - [2.1. 使用 TSS selector 进行任务切换](#21-使用-tss-selector-进行任务切换)
  - [2.2. 使用 Task-gate 进行任务切换](#22-使用-task-gate-进行任务切换)
  - [2.3. 使用 IRET 进行任务切换](#23-使用-iret-进行任务切换)
- [3. VMM 处理任务切换](#3-vmm-处理任务切换)
- [4. 恢复 guest 运行](#4-恢复-guest-运行)
- [5. 示例](#5-示例)

<!-- /code_chunk_output -->

# 1. 任务切换以及 VMM 的模拟

在 `VMX non-root operation` 模式内, 尝试 **TSS 机制**的**任务切换**会产生 `VM-exit`. 包括了下面的途径:

* guest 使用 `TSS selector` 或者 `Task-gate selector` 作为 **far pointer** 来执行 **CALL**、**JMP** 指.

* guest 执行 **IRET** 指令时, 由于 `EFLAGS.NT=1` 而发起任务切换.

* 在一个**向量事件**(中断或异常)**delivery 期间**尝试**任务切换**, 即向量号在 **guest IDT** 对应的**描述符**是 `task-gate` 描述符.

由于 **VMX 不支持 guest 进行任务切换**(利用 **TSS** 机制), 因此在 guest 由于尝试进行任务切换而产生 VM-exit 时, VMM 需要**模拟 guest 的任务切换**来代替处理器的操作(guest 运行在 **legacy 模式**下).

VMM 整个模拟任务切换的过程大致可以分为三个阶段

(1) 处理器检查是否满足任务切换条件阶段.

(2) VMM 模拟处理器任务切换阶段.

(3) VMM 根据情况进行 guest 相应恢复执行阶段.

其中, 在第 2 阶段的任务切换处理非常烦琐. 而在第 3 阶段里, VMM 需要根据情况反射一个异常给 guest 处理, 或者跳转去执行 guest 的新任务. 第 1 阶段的工作由处理器自动完成, 主要是检查是否允许进行任务切换.

# 2. 检查任务切换条件

进行 TSS 任务切换是一个非常复杂的过程, 在 Iong-mode (A-32c)模式下处理器不支持 TSS 的任务切换机制. 在由于任务切换而导致 Vm-exit 之前, 处理器进行一系列的检查, 确认满足任务切换的条件. 如果检查不通过则引发异常, 如果检查通过则产生  Vm-exit.

## 2.1. 使用 TSS selector 进行任务切换

CALL 或 JMP 指令使用 TSS selector 作为 far pointer 进行任务切换时, 处理器进行下面的检查.

(1) 检查 selector 是否为 NULL selector. 属于 NULL selector 时产生#GP 异常.

(2) 检查 selector 是否超过描述符表的 limit. 超过 limit 则产生#GP 异常.

(3) 读取描述符进行类型检查:

* 属于 TSS 描述符, 在 1A-32e 模式下则产生 #GP 异常

* 属于其他不支持的类型(非代码段描述符、TSS 描述符以及 Task-gate 描述符), 则产生#GP 异常.

(4) 检査访问权限. 假如 TSS 描述符的 DPL 小于 CPL 或者 RPL, 则产生 GP 异常.

(5) 检查 TSS selector 的 T 位(bit2). TI=1 时产生 GP 异常.

(6) 检査 TSS 描述符是否为 busy. 属于 busy 则产生#GP 异常.

(7) 检查 TSS 描述符是否为 present. 属于 not-present 则产生#NP 异常.

## 2.2. 使用 Task-gate 进行任务切换

CALL 或 JMP 指令使用 Task- gate selector 作为 far pointer 尝试任务切换. 中断、异常或注入事件 delivery 期间引用 IDT 的 ask-gate 描述符时, 处理器进行下面的检查.

(1) CALL 或 JMP 指令检查 Task- gate selector

* 检查 Task gate selector 是否为 NULL selector. 属于 NULL selector 时产生 GP 异

* 检查 Task- gate selector 是否超过描述符表的 limit. 超过 limit I 时产生 GP 异常.

(2) 中断、异常或者注入事件 deliver I 时检查向量号.

* 检查向量号是否超过 IDT limit. 超过 IDT limit 时产生#GP 异常.

(3) 根据 Task-gate selector 读取 Task-gate 描述符检查

* 在 IA-32e 模式下产生 `#GP` 异常.

(4) 检访问权限, 产生或者注入的 NMI、外部中断、硬件异常不需要检查权限.

* 产生或者注入软件中断或软件异常(INT、INT3 及 INTO): Task-gate 描述符的 DPL 小于 CPL 时, 产生 P 异常.

* 由 CALL 或 JMP 指令发起: Task-gate 描述符的 DPL 小于 CPL 或者 RPL 时, 产生 #GP 异常.

(5) 检查 Task-gate 描述符是否为 present. 属于 not-present 则产生排 NP 异常.

(6) 从 Task-gate 描述符里读取 TSS selector, 检查 TSS selector

* TSS selector I 的 TI 位(bit2) 为 1 时, 产生 GP 异常.

* TSS selector 超过 GDT limit 时, 产生 GP 异常

(7) 根据 rss selector 读取 TSS 描述符检查.

* TSS 描述符属于 busy 时, 产生 #GP 异常.

* TSS 描述符属于 not present 时, 产生 #NP 异常.

## 2.3. 使用 IRET 进行任务切换

当执行 IRET 指令, EFLAGS.NT=1 时, 处理器进行下面的检查.

(1) 在 1A-32e 模式下, 则产生 #GP 异常.

(2) 从当前 TSS 块内的 Task-link 读取 TSS selector 检查 TSS selector 的 T 位(bit2) 为 1 时, 产生#GP 异常  TSS selector 超过 GDT limit 时, 产生#GP 异常.

(3) 根据 TSS selector 读取 TSS 描述符检查.

* 如果不是 TSS 描述符, 产生#TS 异常.

* TSS 描述符不属于 busy 时, 产生#S 异常.

(4) TSS 描述符属于 not- present 时, 产生 NP 异常.

在对上面三个途径下的任务切换进行相应的检查后, 处理器确认允许执行任务切换的条件满足. 但是如前面所述, VMX non- root operation 下不允许进行任务切换. 因此接着会产生 VM-exit

# 3. VMM 处理任务切换

guest 在尝试任务切换时, 经过前面的满足条件检查后产生 VM-exit. VMM 拦截了  guest 任务切换动作, 但是 VMM 必须支持 guest Os 进行任务切换. 因此, 在这个阶段里 VMM 的任务是帮助 guest 完成任务切换操作.

不能通过事件注入方式反射事件给 guest 处理, VMM 应该模拟处理器的任务切换动作. 在 `\lib\Vmx\VmxVMM.asm` 文件里实现了 `DoTaskSwitch` 函数处理 guest 的任务切换.

```x86asm
; DoTaskSwitch()
; input:
;       none
; output:
;       none
; 描述:
;       1) 处理由 task switch 引发的 VM-exit
;-----------------------------------------------------------------------
DoTaskSwitch:
        push ebp
        push ecx
        push edx
        push ebx

%ifdef __X64
        LoadGsBaseToRbp
%else
        mov ebp, [gs: PCB.Base]
%endif

        DEBUG_RECORD    "[DoTaskSwitch]: the VMM to complete the task switching"

        ;;
        ;; 收集任务切换 VM-exit 的相关信息
        ;;
        call GetTaskSwitchInfo

        ;;
        ;; ### VMM 需要模拟处理器的任务切换动作 ###
        ;; 注意:
        ;;  1) 不能使用事件注入重启任务切换！
        ;;  2) 这里的"当前"指"旧任务"
        ;;

        mov ecx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.Source]            ;; 读发起源

        ;;
        ;; step 1: 处理当前的 TSS 描述符
        ;; a) JMP,  IRET 指令发起: 则清 busy 位.
        ;; b) CALL, 中断或异常发起: 则 busy 位保持不变(原 busy 为 1)
        ;;
DoTaskSwitch.Step1:
        cmp ecx, TASK_SWITCH_JMP
        je DoTaskSwitch.Step1.ClearBusy
        cmp ecx, TASK_SWITCH_IRET
        jne DoTaskSwitch.Step2

DoTaskSwitch.Step1.ClearBusy:
        ;;
        ;; 清当前 TSS 描述符 busy 位
        ;;
        REX.Wrxb
        mov ebx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.CurrentTssDesc]
        btr DWORD [ebx + 4], 9


        ;;
        ;; step 2: 在当前 TSS 里保存 context 信息
        ;;
DoTaskSwitch.Step2:
        REX.Wrxb
        mov ebx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.CurrentTss]  ;; 当前 TSS 块
        REX.Wrxb
        mov edx, [ebp + PCB.CurrentVmbPointer]
        REX.Wrxb
        mov edx, [edx + VMB.VsbBase]                                      ;; 当前 VM store block

        ;;
        ;; 将 VSB 保存的 guest context 复制到当前 TSS 块
        ;;
        mov eax, [edx + VSB.Rax]
        mov [ebx + TSS32.Eax], eax                                       ;; 保存 eax
        mov eax, [edx + VSB.Rcx]
        mov [ebx + TSS32.Ecx], eax                                       ;; 保存 ecx
        mov eax, [edx + VSB.Rdx]
        mov [ebx + TSS32.Edx], eax                                       ;; 保存 edx
        mov eax, [edx + VSB.Rbx]
        mov [ebx + TSS32.Ebx], eax                                       ;; 保存 ebx
        mov eax, [edx + VSB.Rsp]
        mov [ebx + TSS32.Esp], eax                                       ;; 保存 esp
        mov eax, [edx + VSB.Rbp]
        mov [ebx + TSS32.Ebp], eax                                       ;; 保存 ebp
        mov eax, [edx + VSB.Rsi]
        mov [ebx + TSS32.Esi], eax                                       ;; 保存 esi
        mov eax, [edx + VSB.Rdi]
        mov [ebx + TSS32.Edi], eax                                       ;; 保存 edi
        mov eax, [edx + VSB.Rflags]
        mov [ebx + TSS32.Eflags], eax                                    ;; 保存 eflags

        ;;
        ;; 注意: 保存 EIP 时需要加上指令长度
        ;;
        mov eax, [edx + VSB.Rip]
        add eax, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.InstructionLength]
        mov [ebx + TSS32.Eip], eax                                       ;; 保存 eip

        ;;
        ;; 读取 guest selector 与 CR3 保存在当前 TSS 里
        ;;
        GetVmcsField    GUEST_CS_SELECTOR
        mov [ebx + TSS32.Cs], ax                                         ;; 保存 cs selector
        GetVmcsField    GUEST_ES_SELECTOR
        mov [ebx + TSS32.Es], ax                                         ;; 保存 es selector
        GetVmcsField    GUEST_DS_SELECTOR
        mov [ebx + TSS32.Ds], ax                                         ;; 保存 ds selector
        GetVmcsField    GUEST_SS_SELECTOR
        mov [ebx + TSS32.Ss], ax                                         ;; 保存 ss selector
        GetVmcsField    GUEST_FS_SELECTOR
        mov [ebx + TSS32.Fs], ax                                         ;; 保存 fs selector
        GetVmcsField    GUEST_GS_SELECTOR
        mov [ebx + TSS32.Gs], ax                                         ;; 保存 gs selector
        GetVmcsField    GUEST_LDTR_SELECTOR
        mov [ebx + TSS32.LdtrSelector], ax                               ;; 保存 ldt selector
        GetVmcsField    GUEST_CR3
        mov [ebx + TSS32.Cr3], eax                                       ;; 保存 cr3


        ;;
        ;; step 3: 处理当前 TSS 内的 eflags.NT 标志位
        ;; a) IRET 指令发起: 则清 TSS 内 eflags.NT 位
        ;; b) CALL, JMP, 中断或异常发起: TSS 内 eflags.NT 位保持不变
        ;;
DoTaskSwitch.Step3:
        cmp ecx, TASK_SWITCH_IRET
        jne DoTaskSwitch.Step4
        ;;
        ;; 清当前 TSS 内的 eflags.NT 位
        ;;
        btr DWORD [ebx + TSS32.Eflags], 14


        ;;
        ;; step 4: 处理目标 TSS 的 eflags.NT 位
        ;; a) CALL, 中断或异常发起: 置 TSS 内的 eflags.NT 位
        ;; b) IRET, JMP 发起: 保持 TSS 内的 eflags.NT 位不变
        ;;
DoTaskSwitch.Step4:
        REX.Wrxb
        mov ebx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.NewTaskTss]          ;; 目标 TSS 块

        cmp ecx, TASK_SWITCH_CALL
        je DoTaskSwitch.Step4.SetNT
        cmp ecx, TASK_SWITCH_GATE
        jne DoTaskSwitch.Step5

DoTaskSwitch.Step4.SetNT:
        ;;
        ;; 置目标 TSS 的 eflags.NT 位
        ;;
        bts DWORD [ebx + TSS32.Eflags], 14

        ;;
        ;; step 5: 处理目标 TSS 描述符
        ;; a) CALL, JMP, 中断或异常发起: 置 busy 位
        ;; b) IRET 发起: busy 位保持不变
        ;;
DoTaskSwitch.Step5:
        cmp ecx, TASK_SWITCH_IRET
        je DoTaskSwitch.Step6
        ;;
        ;; 置目标 TSS 描述符 busy 位
        ;;
        REX.Wrxb
        mov ebx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.NewTaskTssDesc]
        bts DWORD [ebx + 4], 9

        ;;
        ;; step 6: 加载目标 TR 寄存器
        ;;
DoTaskSwitch.Step6:
        REX.Wrxb
        mov edx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.NewTaskTssDesc]    ;; 目标 TSS 描述符
        mov eax, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.NewTrSelector]     ;; 目标 TSS selector

        SetVmcsField    GUEST_TR_SELECTOR, eax
        movzx eax, WORD [edx]                                   ;; 读取 limit
        SetVmcsField    GUEST_TR_LIMIT, eax                     ;; 设置 TR.limit
        movzx eax, WORD [edx + 5]                               ;; 读取 access rights
        and eax, 0F0FFh
        SetVmcsField    GUEST_TR_ACCESS_RIGHTS, eax             ;; 设置 TR access rights
        mov eax, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.NewTaskTssBase]
        SetVmcsField    GUEST_TR_BASE, eax                      ;; 设置 TR base


        ;;
        ;; step 7: 加载目标任务 context
        ;;
DoTaskSwitch.Step7:
        REX.Wrxb
        mov ebx, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.NewTaskTss]        ;; 目标 TSS 块
        REX.Wrxb
        mov edx, [ebp + PCB.CurrentVmbPointer]
        REX.Wrxb
        mov edx, [edx + VMB.VsbBase]                    ;; 当前 VM store block

        ;;
        ;; 将目标 TSS 内的值复制到当前 VSB 内
        ;;
        mov eax, [ebx + TSS32.Eax]
        mov [edx + VSB.Rax], eax                        ;; 加载 eax
        mov eax, [ebx + TSS32.Ecx]
        mov [edx + VSB.Rcx], eax                        ;; 加载 ecx
        mov eax, [ebx + TSS32.Edx]
        mov [edx + VSB.Rdx], eax                        ;; 加载 edx
        mov eax, [ebx + TSS32.Ebx]
        mov [edx + VSB.Rbx], eax                        ;; 加载 ebx
        mov eax, [ebx + TSS32.Ebp]
        mov [edx + VSB.Rbp], eax                        ;; 加载 ebp
        mov eax, [ebx + TSS32.Esi]
        mov [edx + VSB.Rsi], eax                        ;; 加载 esi
        mov eax, [ebx + TSS32.Edi]
        mov [edx + VSB.Rdi], eax                        ;; 加载 edi

        ;;
        ;; 设置 guest ESP, EIP, EFLAGS, CR3
        ;;
        mov eax, [ebx + TSS32.Esp]
        SetVmcsField    GUEST_RSP, eax                  ;; 加载 esp
        mov eax, [ebx + TSS32.Cr3]
        SetVmcsField    GUEST_CR3, eax                  ;; 加载 cr3
        mov eax, [ebx + TSS32.Eip]
        SetVmcsField    GUEST_RIP, eax                  ;; 加载 eip
        mov eax, [ebx + TSS32.Eflags]
        SetVmcsField    GUEST_RFLAGS, eax               ;; 加载 eflags

        ;;
        ;; 加载 SS
        ;;
        mov esi, [ebx + TSS32.Ss]
        call load_guest_ss_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; 加载 CS
        ;;
        mov esi, [ebx + TSS32.Cs]
        call load_guest_cs_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; 加载 ES
        ;;
        mov esi, [ebx + TSS32.Es]
        call load_guest_es_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; 加载 DS
        ;;
        mov esi, [ebx + TSS32.Ds]
        call load_guest_ds_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; 加载 FS
        ;;
        mov esi, [ebx + TSS32.Fs]
        call load_guest_fs_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; 加载 GS
        ;;
        mov esi, [ebx + TSS32.Gs]
        call load_guest_gs_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; 加载 LDTR
        ;;
        mov esi, [ebx + TSS32.LdtrSelector]
        call load_guest_ldtr_register
        cmp eax, TASK_SWITCH_LOAD_STATE_SUCCESS
        jne DoTaskSwitch.Done

        ;;
        ;; step 8: 在目标 TSS 内保存当前 TR selector
        ;;
DoTaskSwitch.Step8:
        mov eax, [ebp + PCB.GuestExitInfo + TASK_SWITCH_INFO.CurrentTrSelector]
        mov [ebx + TSS32.TaskLink], ax                                  ;; 保存 task link

        ;;
        ;; step 9: 设置 CR0.TS 位
        ;;
DoTaskSwitch.Step9:
        GetVmcsField    GUEST_CR0
        or eax, CR0_TS
        SetVmcsField    GUEST_CR0, eax

DoTaskSwitch.Done:
        mov eax, VMM_PROCESS_RESUME
        pop ebx
        pop edx
        pop ecx
        pop ebp
        ret
```

DoTaskSwitch 函数的工作


# 4. 恢复 guest 运行



# 5. 示例

